
/*
 * How do I read rows from my Artist table in my chinook database from an endpoint?
 *
 * I want to support paging and sorting, in addition to optionally being able to filter on
 * ArtistId or Name, which are the only column my table contains.
 *
 * I want you to return ONLY the Hyperlambda code, without comments, and I want you to return the result
 * as a simple array.
 */
.arguments
   limit:long
   offset:long
   order:string
   direction:string
   Artist.ArtistId.eq:long
   Artist.Name.like:string
   Artist.Name.eq:string
data.connect:[generic|chinook]
   database-type:sqlite
   add:x:./*/data.read
      get-nodes:x:@.arguments/*/order
      get-nodes:x:@.arguments/*/direction
   remove-nodes:x:@.arguments/*/order
   remove-nodes:x:@.arguments/*/direction
   add:x:./*/data.read
      get-nodes:x:@.arguments/*/limit
      get-nodes:x:@.arguments/*/offset
   remove-nodes:x:@.arguments/*/limit
   remove-nodes:x:@.arguments/*/offset
   add:x:./*/data.read/*/where/*
      get-nodes:x:@.arguments/*
   data.read
      database-type:sqlite
      table:Artist
      columns
         Artist.ArtistId
         Artist.Name
      where
         and
   return-nodes:x:@data.read/*
