
// Create an [else] block that executes the [magic.ai.functions.invoke] slot with arguments from [.type], [.session], [.user-id], [.dp/#], and [.extra]. Convert the result of the invocation (found at [@execute/*/result/*]) to JSON using [lambda2json], with the [format] argument set to true for pretty printing. Then, check if the number of tokens in the JSON result (using [openai.tokenize]) exceeds the [max_context_tokens] argument; if so, throw an error with a public 400 status. Otherwise, retrieve a success message from configuration using [config.get] and unwrap its value, then signal the client via [sockets.signal], sending the success message, the raw JSON result, and the workflow file. If the result exists, append a formatted JSON response to [.new-prompt] and update [.function-result] with the JSON result. If the result does not exist, update [.new-prompt] with a generic success message referencing the workflow.
else
   execute:magic.ai.functions.invoke
      type:x:@.type
      session:x:@.session
      user-id:x:@.user-id
      invocation:x:@.dp/#
      extra:x:@.extra
   lambda2json:x:@execute/*/result/*
      format:true
   if
      mt
         openai.tokenize:x:@lambda2json
         get-value:x:@.arguments/@.arguments/*/max_context_tokens
      .lambda
         throw:Result too large, try to limit your result
            status:int:400
            public:bool:true
   config.get:"magic:chat:functions:success-message"
      .:Success!
   unwrap:x:+/*/*
   sockets.signal:x:@.session
      args
         function_result:x:@config.get
         invocation:x:@execute/*/json
         file:x:@execute/*/workflow
   if
      exists:x:@execute/*/result/*
      .lambda
         set-value:x:@.new-prompt
            strings.concat
               get-value:x:@.new-prompt
               .:"Response from '"
               get-value:x:@execute/*/workflow
               .:@"' was:
```json
"
               get-value:x:@lambda2json
               .:@"
"
               .:```
               .:@"
"
               .:@"
"
         set-value:x:@.function-result
            strings.concat
               get-value:x:@.function-result
               get-value:x:@lambda2json
               .:"\n"
   else
      set-value:x:@.new-prompt
         strings.concat
            get-value:x:@.new-prompt
            .:"Invocation of '"
            get-value:x:@execute/*/workflow
            .:"' was a success."
            .:@"
"
            .:@"
"
