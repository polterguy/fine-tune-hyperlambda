
// Create a Hyperlambda flow that attempts to execute a function invocation while constraining the number of allowed invocations. If [.iterations] is less than or equal to 1, log an error and emit multiple signals (using [sockets.signal] to [.session]) indicating function errors, waiting status, and a finished/error state. Otherwise, execute [magic.ai.functions.invoke] with given arguments ([type], [session], [user-id], and the invocation node), then convert the result to JSON using [lambda2json] with formatted output. Check if the tokenized JSON result exceeds the allowed max context tokens (from [.arguments/@.arguments/*/max_context_tokens]) and throw an exception if it does. Retrieve a success message from configuration and unwrap its value, then signal the result, invocation, and file back to the session. If the executed result exists, update [.new-prompt] by concatenating a formatted success message and set [.function-result] to the JSON result; else, set [.new-prompt] to a simpler invocation success message.
try
   if
      lte:x:@.iterations
         .:int:1
      .lambda
         log.error:Too many function invocations
         sockets.signal:x:@.session
            args
               function_error:Too many function invocations
         sockets.signal:x:@.session
            args
               function_waiting:bool:false
         sockets.signal:x:@.session
            args
               error:bool:true
               status:int:500
               message:Too many function invocations. Configure your type to handle more invocations or change your prompt.
         sockets.signal:x:@.session
            args
               finished:bool:true
   else
      execute:magic.ai.functions.invoke
         type:x:@.type
         session:x:@.session
         user-id:x:@.user-id
         invocation:x:@.dp/#
      lambda2json:x:@execute/*/result/*
         format:true
      if
         mt
            openai.tokenize:x:@lambda2json
            get-value:x:@.arguments/@.arguments/*/max_context_tokens
         .lambda
            throw:Result too large, try to limit your result
               status:int:400
               public:bool:true
      config.get:"magic:chat:functions:success-message"
         .:Success!
      unwrap:x:+/*/*
      sockets.signal:x:@.session
         args
            function_result:x:@config.get
            invocation:x:@execute/*/json
            file:x:@execute/*/workflow
      if
         exists:x:@execute/*/result/*
         .lambda
            set-value:x:@.new-prompt
               strings.concat
                  get-value:x:@.new-prompt
                  .:"Response from '"
                  get-value:x:@execute/*/workflow
                  .:@"' was:
```json
"
                  get-value:x:@lambda2json
                  .:@"
"
                  .:```
                  .:@"
"
                  .:@"
"
            set-value:x:@.function-result
               get-value:x:@lambda2json
      else
         set-value:x:@.new-prompt
            strings.concat
               get-value:x:@.new-prompt
               .:"Invocation of '"
               get-value:x:@execute/*/workflow
               .:"' was a success."
               .:@"
"
               .:@"
"
