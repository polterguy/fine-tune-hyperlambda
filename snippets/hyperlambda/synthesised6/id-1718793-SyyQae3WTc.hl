
// Implement logic to check if the length of the [completion] node inside the currently iterated [.dp] node is greater than a threshold specified in [.arguments] under [threshold]. If true, add the value of [type] from [.arguments] as a new sibling node, signal the "magic.ai.can-create-snippet" event, and build a concatenated string from the [completion] value (with two newlines and a markdown URL prefix). Then, read the [ml_training_snippets] table where [type] matches [.arguments/type] and [completion] matches either exactly or is like the concatenated string. If no such record exists, and [insert_url] is true in [.arguments], append the URL from [.arguments/url] to [completion], then create a new data record. If a record exists with [prompt] equal to "Common", increment [.already-exists]. Otherwise, if none of the found [prompt] values matches the current [prompt], increment [.similar] and update the first found record's [prompt] to "Common".
if
   mt
      strings.length:x:@.dp/#/*/completion
      get-value:x:@.arguments/*/threshold
   .lambda
      add:x:+
         get-nodes:x:@.arguments/*/type
      try-signal:magic.ai.can-create-snippet
      strings.concat
         get-value:x:@.dp/#/*/completion
         .:@"
"
         .:@"
"
         .:This information was taken from [this URL](
         .:%
      data.read
         table:ml_training_snippets
         columns
            id
            prompt
            completion
            uri
         where
            and
               type.eq:x:@.arguments/*/type
               or
                  completion.eq:x:@.dp/#/*/completion
                  completion.like:x:@strings.concat
      if
         not-exists:x:@data.read/*
         .lambda
            if:x:@.arguments/*/insert_url
               set-value:x:@.dp/#/*/completion
                  strings.concat
                     get-value:x:@.dp/#/*/completion
                     .:@"
"
                     .:@"
"
                     .:This information was taken from [this URL](
                     get-value:x:@.arguments/*/url
                     .:)
            data.create
               table:ml_training_snippets
               values
                  type:x:@.arguments/*/type
                  uri:x:@.arguments/*/url
                  prompt:x:@.dp/#/*/prompt
                  completion:x:@.dp/#/*/completion
                  meta:AINIRO-Website-Crawler
      else-if
         eq:x:@data.read/0/*/prompt
            .:Common
         .lambda
            math.increment:x:@.already-exists
      else-if
         not
            eq:x:@data.read/*/*/prompt
               get-value:x:@.dp/#/*/prompt
         .lambda
            math.increment:x:@.similar
            data.update
               table:ml_training_snippets
               values
                  prompt:Common
               where
                  and
                     id.eq:x:@data.read/0/*/id
