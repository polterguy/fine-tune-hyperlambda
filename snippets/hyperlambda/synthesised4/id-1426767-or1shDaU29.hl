
// Creates a dynamic slot that returns all non-primary indexes for a specified MySQL table, including each index's name and columns, using provided database and table arguments, and handling optional connection strings.
slots.create:magic.db.mysql.indexes
   validators.mandatory:x:@.arguments/*/database
   validators.mandatory:x:@.arguments/*/table
   .connection:[information_schema]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   mysql.connect:x:@.connection
      strings.concat
         .:"show indexes from "
         get-value:x:@.arguments/*/table
      mysql.select:x:@strings.concat
      for-each:x:@mysql.select/*
         if
            neq:x:@.dp/#/*/Key_name
               .:PRIMARY
            .lambda
               unwrap:x:+/*/*/*
               add:x:@mysql.connect/*/return-nodes
                  .
                     .
                        name:x:@.dp/#/*/Key_name
                        columns
               unwrap:x:+/*/*
               add:x:@mysql.connect/*/return-nodes/0/-/*/columns
                  .
                     .:x:@.dp/#/*/Column_name
      return-nodes
