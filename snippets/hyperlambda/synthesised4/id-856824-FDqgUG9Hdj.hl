
// This Hyperlambda code connects to a Microsoft SQL Server database and retrieves metadata about columns in a specified table. It uses [mssql.connect] to establish the connection and [mssql.select] to execute a query that fetches column details like name, type, and constraints. The [for-each] loop iterates over the results, using [unwrap] and [switch] to map SQL data types to Hyperlambda types. The code also checks for primary keys and auto-increment fields, and returns the processed metadata as a node list.
mssql.connect:x:@.connection
   mssql.select:"select c.name 'name', t.Name 'type', c.is_nullable 'nullable', isnull(i.is_primary_key, 0) 'primary', c.is_identity 'automatic', c.default_object_id 'default'\n         from sys.columns c \n             inner join sys.types t on c.user_type_id = t.user_type_id\n             left outer join sys.index_columns ic \n             left outer join sys.indexes i on ic.object_id = i.object_id and \n                 ic.index_id = i.index_id on ic.object_id = c.object_id and \n                 ic.column_id = c.column_id and i.is_primary_key = 1\n         where c.object_id = OBJECT_ID(@table)"
      @table:x:@.arguments/*/table
   for-each:x:@mssql.select/*
      unwrap:x:+/*/*
      .tmp
         ""
            name:x:@.dp/#/*/name
            db:x:@.dp/#/*/type
            nullable:x:@.dp/#/*/nullable
            primary:x:@.dp/#/*/primary
            automatic:x:@.dp/#/*/automatic
      if
         mt
            get-value:x:@.dp/#/*/default
            .:int:0
         .lambda
            set-value:x:@.tmp/*/*/automatic
               .:bool:true
      .type
      switch:x:@.dp/#/*/type
         case:date
         case:datetime
         case:datetime2
         case:datetimeoffset
         case:smalldatetime
         case:time
            set-value:x:@.type
               .:date
         case:numeric
         case:decimal
         case:smallmoney
         case:money
         case:float
         case:real
            set-value:x:@.type
               .:decimal
         case:bigint
         case:smallint
         case:int
         case:tinyint
            set-value:x:@.type
               .:long
         case:bit
            set-value:x:@.type
               .:bool
         case:char
         case:varchar
         case:text
         case:nchar
         case:nvarchar
         case:ntext
         case:binary
         case:image
         case:varbinary
         case:uniqueidentifier
            set-value:x:@.type
               .:string
         case:rowversion
         case:timestamp
            set-value:x:@.type
               .:string
            set-value:x:@.tmp/*/*/automatic
               .:bool:true
         default
            set-value:x:@.type
               .:string
      unwrap:x:+/*/*
      add:x:@.tmp/*
         .
            hl:x:@.type
      add:x:../*/mssql.connect/*/return-nodes
         get-nodes:x:@.tmp/*
   return-nodes
