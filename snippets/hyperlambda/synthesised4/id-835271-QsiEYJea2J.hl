
// This Hyperlambda code performs a series of operations based on certain conditions. It first checks if the name argument is "generic" and if the useAsDefault argument exists and is true. If so, it retrieves user information from a database and updates configuration settings. It then deletes and recreates user records in the database, including roles, and clears the cache. Key slots include [if], [and], [eq], [exists], [get-value], [data.connect], [data.read], [data.create], [data.delete], [for-each], [set-value], [lambda2json], [config.save], [sleep], [io.file.execute], and [cache.clear].
if
   and
      eq:x:@.arguments/*/name
         .:generic
      exists:x:@.arguments/*/useAsDefault
      get-value:x:@.arguments/*/useAsDefault
   .lambda
      .username
      .password
      .email
      .name
      auth.ticket.get
      data.connect:[generic|magic]
         data.read
            table:users
               join:users_extra
                  as:ue1
                  on
                     and
                        users.username.eq:ue1.user
               join:users_extra
                  as:ue2
                  on
                     and
                        users.username.eq:ue2.user
            columns
               users.username
               users.password
               ue1.value
                  as:email
               ue2.value
                  as:name
            where
               and
                  ue1.type.eq:email
                  ue2.type.eq:name
                  users.username.eq:x:@auth.ticket.get
         set-value:x:@.username
            get-value:x:@data.read/*/*/username
         set-value:x:@.password
            get-value:x:@data.read/*/*/password
         set-value:x:@.email
            get-value:x:@data.read/*/*/email
         set-value:x:@.name
            get-value:x:@data.read/*/*/name
      set-value:x:@json2lambda/*/magic/*/databases/*/default
         get-value:x:@.arguments/*/databaseType
      lambda2json:x:@json2lambda/*
      config.save:x:-
      sleep:2000
      io.file.execute:/system/ensure-magic-database.hl
      data.connect:[generic|magic]
         data.delete
            table:users
            where
               and
                  username.eq:x:@.username
         data.create
            table:users
            values
               username:x:@.username
               password:x:@.password
         data.create
            table:users_extra
            values
               user:x:@.username
               type:email
               value:x:@.email
         data.create
            table:users_extra
            values
               user:x:@.username
               type:name
               value:x:@.name
         for-each:x:@auth.ticket.get/*/roles/*
            data.create
               table:users_roles
               values
                  user:x:@.username
                  role:x:@.dp/#
      cache.clear
