
// This Hyperlambda code iterates over text nodes, replacing specific whitespace characters with a single space. It checks if the node is an anchor tag with a valid href attribute, then constructs a complete URL based on conditions, appending it to a completion string. The code ensures that URLs are correctly formatted and text is cleaned of excessive whitespace.
for-each:x:"@.dp/#/**/\\#text"
   if
      and
         not-null:x:@.dp/#
         neq:x:@.dp/#
            .:
      .lambda
         strings.replace:x:@.dp/#
            .:"\r"
            .:" "
         strings.replace:x:@strings.replace
            .:"\n"
            .:" "
         strings.replace:x:@strings.replace
            .:"\t"
            .:" "
         strings.replace:x:@strings.replace
            .:"  "
            .:" "
         if
            and
               eq
                  get-name:x:@.dp/#/.
                  .:a
               exists:x:"@.dp/#/./*/\\@href/[0,1]"
               not-null:x:"@.dp/#/./*/\\@href/[0,1]"
            .lambda
               .url
               if
                  strings.starts-with:x:"@.dp/#/./*/\\@href/[0,1]"
                     .:/
                  .lambda
                     set-value:x:@.url
                        strings.concat
                           get-value:x:@.base
                           get-value:x:"@.dp/#/./*/\\@href/[0,1]"
               else-if
                  and
                     not
                        strings.starts-with:x:"@.dp/#/./*/\\@href/[0,1]"
                           .:"http://"
                     not
                        strings.starts-with:x:"@.dp/#/./*/\\@href/[0,1]"
                           .:"https://"
                  .lambda
                     set-value:x:@.url
                        strings.concat
                           get-value:x:@.base
                           .:/
                           get-value:x:"@.dp/#/./*/\\@href/[0,1]"
               else
                  set-value:x:@.url
                     get-value:x:"@.dp/#/./*/\\@href/[0,1]"
               set-value:x:@.completion
                  strings.concat
                     get-value:x:@.completion
                     .:" "
                     .:[
                     strings.trim:x:@strings.replace
                        .:@"  
	"
                     .:](
                     get-value:x:@.url
                     .:)
                     .:" "
         else
            set-value:x:@.completion
               strings.concat
                  get-value:x:@.completion
                  strings.trim:x:@strings.replace
                     .:@"  
	"
                  .:" "
