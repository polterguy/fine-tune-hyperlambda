
/*
 * // This Hyperlambda code snippet attempts to create a summary of a given text using OpenAI's API. It repeatedly tries to send a request to the OpenAI API until successful or until a certain condition is met. The process involves sending an HTTP POST request with specific headers and payload, handling different response statuses, and signaling feedback through sockets. If an error occurs, it logs the error and retries after a delay.
 * // 1. [while] - Loops while the condition is true.
 * // 2. [mt] - Checks if [.no] is greater than 0.
 * // 3. [try] - Begins a try block to handle exceptions.
 * // 4. [unwrap] - Evaluates all expressions in the node set.
 * // 5. [http.post] - Sends a POST request to the OpenAI API.
 * // 6. [headers] - Sets the HTTP headers for the request.
 * // 7. [payload] - Defines the payload for the POST request.
 * // 8. [if] - Checks if the HTTP response status is 400.
 * // 9. [eq] - Compares the HTTP response status to 400.
 * // 10. [sockets.signal] - Sends a signal through a socket with feedback.
 * // 11. [sleep] - Pauses execution for a specified time.
 * // 12. [lambda2hyper] - Converts a lambda object to a Hyperlambda string.
 * // 13. [log.error] - Logs an error message.
 * // 14. [set-value] - Sets the value of a node.
 * // 15. [else-if] - Checks if the HTTP response status is between 200 and 299.
 * // 16. [and] - Logical AND operation for conditions.
 * // 17. [mte] - Checks if the HTTP response status is greater than or equal to 200.
 * // 18. [lt] - Checks if the HTTP response status is less than 300.
 * // 19. [strings.trim] - Trims whitespace from a string.
 * // 20. [strings.concat] - Concatenates strings.
 * // 21. [strings.substring] - Extracts a substring from a string.
 * // 22. [else] - Default case if no other conditions are met.
 * // 23. [math.decrement] - Decrements the value of [.no] by 1.
 * // 24. [.catch] - Handles exceptions by decrementing [.no].
 */
while
   mt:x:@.no
      .:int:0
   .lambda
      try
         unwrap:x:+/**
         http.post:"https://api.openai.com/v1/chat/completions"
            headers
               Authorization:x:@.token
               Content-Type:application/json
            payload
               model:x:@.model
               max_tokens:x:@.max-size
               temperature:decimal:0.3
               messages
                  .
                     role:system
                     content:Create a summary of the following information
                  .
                     role:user
                     content:x:@.dp/#/*/completion
            convert:true
         if
            eq:x:@http.post
               .:int:400
            .lambda
               sockets.signal:x:@.arguments/*/feedback-channel
                  args
                     message:Could not summarize snippet, check your log for details
                     type:warning
               sleep:100
               lambda2hyper:x:@http.post
               log.error:Something went wrong while invoking OpenAI
                  message:x:@http.post/*/content/*/error/*/message
                  status:x:@http.post
                  error:x:@lambda2hyper
               set-value:x:@.no
                  .:int:0
         else-if
            and
               mte:x:@http.post
                  .:int:200
               lt:x:@http.post
                  .:int:300
            .lambda
               sockets.signal:x:@.arguments/*/feedback-channel
                  args
                     message:Successfully created summary of snippet
                     type:info
               sleep:100
               set-value:x:@.dp/#/*/completion
                  strings.trim:x:@http.post/*/content/*/choices/0/*/message/*/content
                     .:@"
	 "
               set-value:x:@.dp/#/*/prompt
                  strings.concat
                     .:"Summary; "
                     strings.substring:x:@.dp/#/*/prompt
                        .:int:0
                        .:int:150
               set-value:x:@.no
                  .:int:0
         else
            sockets.signal:x:@.arguments/*/feedback-channel
               args
                  message:Could not summarize snippet, check your log for details. Trying again in 3 seconds.
                  type:warning
            sleep:3000
            lambda2hyper:x:@http.post
            log.error:Something went wrong while invoking OpenAI
               message:x:@http.post/*/content/*/error/*/message
               status:x:@http.post
               error:x:@lambda2hyper
            math.decrement:x:@.no
      .catch
         math.decrement:x:@.no
