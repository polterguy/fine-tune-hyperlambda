
/*
 * This Hyperlambda code connects to a data source and processes a prompt and completion logic based on certain conditions. It first checks if the [no-invocation] node value is greater than zero to modify the [prompt] node accordingly. If [function-result] is not null, it appends additional information to [tmp-completion] including a JSON formatted result. Finally, it creates a new entry in the "ml_requests" table with various values sourced from the arguments and processed nodes.
 * 
 * 1. [data.connect] - Connects to a data source.
 * 2. [if] - Checks if [no-invocation] is greater than 0.
 * 3. [set-value] - Sets the value of [prompt] based on the condition.
 * 4. [else] - Sets [prompt] if the condition is false.
 * 5. [if] - Checks if [function-result] is not null.
 * 6. [set-value] - Sets [tmp-completion] with additional function result information.
 * 7. [else] - Sets [tmp-completion] with [result] if [function-result] is null.
 * 8. [data.create] - Creates a new entry in the "ml_requests" table with specified values.
 */
data.connect:[generic|magic]
   .prompt
   if
      mt:x:@.no-invocation
         .:int:0
      .lambda
         set-value:x:@.prompt
            strings.concat
               .:[
               get-value:x:@.no-invocation
               .:"] - "
               get-value:x:@.only-prompt
   else
      set-value:x:@.prompt
         get-value:x:@.only-prompt
   .tmp-completion
   if
      not-null:x:@.function-result
      .lambda
         set-value:x:@.tmp-completion
            strings.concat
               get-value:x:@.result
               .:@"
"
               .:@"
"
               .:@"Result of function invocation:
"
               .:@"
"
               .:```json
               .:@"
"
               get-value:x:@.function-result
               .:@"
"
               .:```
         set-value:x:@.function-result
   else
      set-value:x:@.tmp-completion
         get-value:x:@.result
   data.create
      table:ml_requests
      values
         type:x:@.arguments/*/type
         prompt:x:@.prompt
         completion:x:@.tmp-completion
         finish_reason:x:@.finish_reason
         session:x:@.arguments/*/session
         user_id:x:@.arguments/*/user_id
         referrer:x:@.arguments/*/referrer
