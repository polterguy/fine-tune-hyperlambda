
// This Hyperlambda code defines a dynamic slot [magic.twilio.send-message] for sending messages using Twilio. It validates mandatory arguments like [to], [from], [body], [twilio_account_id], and [twilio_account_sid]. It constructs an authentication header using base64 encoding and prepares the message content by removing markdown links and trimming whitespace. The message is sent via an HTTP POST request to Twilio's API, and the response is checked for success. If successful, it logs the message details; otherwise, it logs an error and throws an exception.
slots.create:magic.twilio.send-message
   validators.mandatory:x:@.arguments/*/to
   validators.mandatory:x:@.arguments/*/from
   validators.mandatory:x:@.arguments/*/body
   validators.mandatory:x:@.arguments/*/twilio_account_id
   validators.mandatory:x:@.arguments/*/twilio_account_sid
   .auth
   set-value:x:@.auth
      strings.concat
         get-value:x:@.arguments/*/twilio_account_id
         .:":"
         get-value:x:@.arguments/*/twilio_account_sid
   set-value:x:@.auth
      convert:x:@.auth
         type:bytes
   set-value:x:@.auth
      strings.concat
         .:"Basic "
         convert:x:@.auth
            type:base64
   .content
   set-value:x:@.content
      strings.regex-replace:x:@.arguments/*/body
         .:"!\\[.*\\]\\(.*\\)"
         .:
   set-value:x:@.content
      strings.trim:x:@.content
   .cont:bool:true
   while:x:@.cont
      if
         not
            strings.contains:x:@.content
               .:@"


"
         .lambda
            set-value:x:@.cont
               .:bool:false
      else
         set-value:x:@.content
            strings.replace:x:@.content
               .:@"


"
               .:@"

"
   set-value:x:@.content
      strings.replace:x:@.content
         .:[
         .:
   set-value:x:@.content
      strings.replace:x:@.content
         .:](
         .:" ("
   .url
   set-value:x:@.url
      strings.concat
         .:"https://api.twilio.com/2010-04-01/Accounts/"
         get-value:x:@.arguments/*/twilio_account_id
         .:/Messages.json
   http.post:x:@.url
      convert:true
      headers
         Content-Type:application/x-www-form-urlencoded
         Authorization:x:@.auth
      payload
         Body:x:@.content
         From:x:@.arguments/*/from
         To:x:@.arguments/*/to
   if
      not
         and
            mte:x:@http.post
               .:int:200
            lt:x:@http.post
               .:int:300
      .lambda
         lambda2hyper:x:@http.post/*/content
         log.error:Something went wrong while invoking Twilio
            response:x:@lambda2hyper
         throw:Something went wrong as we invoked Twilio, check your log for details
   log.info:Successfully sent message using Twilio
      body:x:@.arguments/*/body
      to:x:@.arguments/*/to
      from:x:@.arguments/*/from
      twilio_account_id:x:@.arguments/*/twilio_account_id
