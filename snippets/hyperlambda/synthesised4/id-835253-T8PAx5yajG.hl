
// The code checks if the token count of an embedding is less than 8191 using [openai.tokenize]. If true, it sends a signal with [sockets.signal] and posts the embedding to OpenAI's API with [http.post]. If the response is not a successful HTTP status, it logs an error with [log.error] and signals a warning. It also handles specific HTTP errors with additional signals and pauses execution using [sleep]. If successful, it updates the embedding in the database using [data.connect] and increments a success counter with [math.increment].
if
   lt
      openai.tokenize:x:@.embedding
      .:int:8191
   .lambda
      strings.concat
         .:"Vectorizing: \""
         get-value:x:@.dp/#/*/prompt
         .:"\""
      unwrap:x:+/*/args/*/message
      sockets.signal:x:@.arguments/*/feedback-channel
         args
            message:x:@strings.concat
            type:info
      http.post:"https://api.openai.com/v1/embeddings"
         headers
            Authorization:x:@.token
            Content-Type:application/json
         payload
            model:x:@.vector-model
            input:x:@.embedding
         convert:true
      if
         not
            and
               mte:x:@http.post
                  .:int:200
               lt:x:@http.post
                  .:int:300
         .lambda
            math.increment:x:@.failed
            lambda2hyper:x:@http.post
            log.error:Something went wrong while invoking OpenAI
               message:x:@http.post/*/content/*/error/*/message
               error:x:@lambda2hyper
            strings.concat
               .:"OpenAI failed, could not create vectors for '"
               get-value:x:@.dp/#/*/prompt
               .:"'"
            unwrap:x:+/*/args/*/message
            sockets.signal:x:@.arguments/*/feedback-channel
               args
                  message:x:@strings.concat
                  type:warning
            if
               or
                  eq:x:@http.post
                     .:int:429
                  eq:x:@http.post
                     .:int:500
               .lambda
                  sockets.signal:x:@.arguments/*/feedback-channel
                     args
                        message:Continuing in 5 seconds with the next snippet to let OpenAI cool down first
                        type:info
                  sleep:5000
            else
               sockets.signal:magic.backend.message
                  roles:root
                  args
                     message:Could not vectorize snippet and it was a non-transitional error, aborting vectorization for type
                     type:error
               set-value:x:@.continue
                  .:bool:false
      else
         .embedding
         set-value:x:@.embedding
            strings.concat
               .:[
               strings.join:x:@http.post/*/content/*/data/0/*/embedding/*
                  .:,
               .:]
         data.connect:[generic|magic]
            data.update
               table:ml_training_snippets
               values
                  embedding:x:@.embedding
               where
                  and
                     id.eq:x:@.dp/#/*/id
         math.increment:x:@.success
