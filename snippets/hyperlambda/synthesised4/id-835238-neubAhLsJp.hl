
// This Hyperlambda code processes data from a CSV converted to a lambda object, generating prompt-completion pairs for machine learning training. It iterates through each node in the lambda object, constructing a prompt from the first element and a completion from subsequent elements, trimming the completion, and storing these pairs in a database table named "ml_training_snippets". The code ensures non-null and non-empty values are included in the completion.
data.connect:[generic|magic]
   for-each:x:@csv2lambda/*
      .prompt:
      .completion:
      set-value:x:@.prompt
         strings.concat
            get-name:x:@.dp/#/0
            .:": "
            get-value:x:@.dp/#/0
      for-each:x:@.dp/#/*/[1,1000]
         if
            and
               not-null:x:@.dp/#
               neq:x:@.dp/#
                  .:
            .lambda
               set-value:x:@.completion
                  strings.concat
                     get-value:x:@.completion
                     .:@"
"
                     .:@"
"
                     get-name:x:@.dp/#
                     .:": "
                     get-value:x:@.dp/#
      set-value:x:@.completion
         strings.trim:x:@.completion
      data.create
         table:ml_training_snippets
         values
            type:x:@.arguments/*/type
            prompt:x:@.prompt
            completion:x:@.completion
            meta:x:@.arguments/*/file/*/name
