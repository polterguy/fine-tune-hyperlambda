
/*
 * The code snippet is a Hyperlambda script that attempts to create a summary of a given text using OpenAI's API. It uses a [while] loop to repeatedly try sending a POST request to OpenAI's chat completions endpoint until a successful response is received or the retry limit is reached. The script handles different HTTP response statuses, logging errors and signaling feedback through a socket connection. It also trims and concatenates strings to format the response and manages retry attempts by decrementing a counter in case of failures.
 * 
 * 1. [while] - Repeats the execution of its child nodes while the condition is true.
 * 2. [mt] - Checks if the [.no] node is more than 0.
 * 3. [try] - Attempts to execute the HTTP POST request and subsequent logic.
 * 4. [unwrap] - Evaluates the expression to prepare for HTTP POST execution.
 * 5. [http.post] - Sends a POST request to OpenAI's API for text summarization.
 * 6. [if] - Checks if the HTTP response status is 400, indicating a client error.
 * 7. [sockets.signal] - Sends a warning message to the feedback channel.
 * 8. [sleep] - Pauses execution for a specified time.
 * 9. [lambda2hyper] - Converts the HTTP response to a Hyperlambda object for logging.
 * 10. [log.error] - Logs an error message if the request fails.
 * 11. [set-value] - Resets or updates node values based on conditions.
 * 12. [else-if] - Checks if the HTTP response status is between 200 and 299, indicating success.
 * 13. [strings.trim] - Trims whitespace from the response content.
 * 14. [strings.concat] - Concatenates strings to format the prompt.
 * 15. [else] - Handles other HTTP response statuses, signaling a retry attempt.
 * 16. [math.decrement] - Decreases the [.no] counter in case of errors.
 * 17. [.catch] - Handles exceptions by decrementing the [.no] counter.
 */
while
   mt:x:@.no
      .:int:0
   .lambda
      try
         unwrap:x:+/**
         http.post:"https://api.openai.com/v1/chat/completions"
            headers
               Authorization:x:@.token
               Content-Type:application/json
            payload
               model:x:@.model
               max_tokens:x:@.max-size
               temperature:decimal:0.3
               messages
                  .
                     role:system
                     content:Create a summary of the following information
                  .
                     role:user
                     content:x:@.dp/#/*/completion
            convert:true
         if
            eq:x:@http.post
               .:int:400
            .lambda
               sockets.signal:x:@.arguments/*/feedback-channel
                  args
                     message:Could not summarize snippet, check your log for details
                     type:warning
               sleep:100
               lambda2hyper:x:@http.post
               log.error:Something went wrong while invoking OpenAI
                  message:x:@http.post/*/content/*/error/*/message
                  status:x:@http.post
                  error:x:@lambda2hyper
               set-value:x:@.no
                  .:int:0
         else-if
            and
               mte:x:@http.post
                  .:int:200
               lt:x:@http.post
                  .:int:300
            .lambda
               sockets.signal:x:@.arguments/*/feedback-channel
                  args
                     message:Successfully created summary of snippet
                     type:info
               sleep:100
               set-value:x:@.dp/#/*/completion
                  strings.trim:x:@http.post/*/content/*/choices/0/*/message/*/content
                     .:@"
	 "
               set-value:x:@.dp/#/*/prompt
                  strings.concat
                     .:"Summary; "
                     strings.substring:x:@.dp/#/*/prompt
                        .:int:0
                        .:int:150
               set-value:x:@.no
                  .:int:0
         else
            sockets.signal:x:@.arguments/*/feedback-channel
               args
                  message:Could not summarize snippet, check your log for details. Trying again in 3 seconds.
                  type:warning
            sleep:3000
            lambda2hyper:x:@http.post
            log.error:Something went wrong while invoking OpenAI
               message:x:@http.post/*/content/*/error/*/message
               status:x:@http.post
               error:x:@lambda2hyper
            math.decrement:x:@.no
      .catch
         math.decrement:x:@.no
