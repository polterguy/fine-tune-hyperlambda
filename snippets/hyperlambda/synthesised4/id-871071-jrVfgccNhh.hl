
// This Hyperlambda code connects to a database and selects records from the "hyperlambda" table based on specific conditions, including the presence of ".arguments" in the code. It iterates over the selected records, converting Hyperlambda code to a lambda object and back to Hyperlambda, while logging and executing OpenAI workflows. The results are saved to files in a specified directory. Key slots include [data.connect] for database connection, [data.select] for querying, [for-each] for iteration, [hyper2lambda] and [lambda2hyper] for transformations, and [execute] for invoking workflows.
data.connect:fine-tune
   data.select:"select * from hyperlambda where code like '%.arguments%' and is_static = 0 and synthesised = 0 limit @limit offset @offset"
      @limit:x:@.arguments/*/limit
      @offset:x:@.arguments/*/offset
   for-each:x:@data.select/*
      hyper2lambda:x:@.dp/#/*/code
         comments:true
      unwrap:x:+/*/*
      insert-before:x:@hyper2lambda/0
         .
            ..:x:@.dp/#/*/prompt
      .hl
      set-value:x:@.hl
         lambda2hyper:x:@hyper2lambda/*
            comments:true
      log.info:Invoking OpenAI
         prompt:x:@.hl
      execute:magic.workflows.actions.execute
         name:openai-context-from-database
         filename:/modules/openai/workflows/actions/openai-context-from-database.hl
         arguments
            type:magic-documentation
            query:x:@.hl
            threshold:decimal:0.3
            max_tokens:int:3000
      execute:magic.workflows.actions.execute
         name:openai-query
         filename:/modules/openai/workflows/actions/openai-query.hl
         arguments
            model:gpt-4o
            max_tokens:int:1500
            instruction:x:@.system-message
            context:x:@execute/@execute/*/context
            query:x:@.hl
      .filename
      set-value:x:@.filename
         strings.concat
            .:/modules/fine-tune-hyperlambda/snippets/hyperlambda/synthesised/
            get-value:x:@.dp/#/*/hyperlambda_id
            .:.hl
      io.file.save:x:@.filename
         get-value:x:@execute/*/answer
      sleep:5000
