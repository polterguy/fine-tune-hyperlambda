
// This Hyperlambda code connects to a database and performs a series of operations on the "ml_training_snippets" table. It adds conditions to the WHERE clause for both read and delete operations, removes specific nodes, and checks for the existence of certain files, deleting them if they exist. It reads data to generate SQL for deleting records with specific row IDs, executes the SQL, and finally deletes records from the table, returning the number of affected rows. The code ensures that operations are only performed if certain conditions are met, such as the non-existence of specific nodes.
data.connect:[generic|magic]
   add:x:./*/data.read/*/where/*/*/or
      get-nodes:x:@.arguments/*/ml_training_snippets.prompt.like
      get-nodes:x:@.arguments/*/ml_training_snippets.completion.like
      get-nodes:x:@.arguments/*/ml_training_snippets.uri.like
   add:x:./*/data.delete/*/where/*/*/or
      get-nodes:x:@.arguments/*/ml_training_snippets.prompt.like
      get-nodes:x:@.arguments/*/ml_training_snippets.completion.like
      get-nodes:x:@.arguments/*/ml_training_snippets.uri.like
   remove-nodes:x:@.arguments/*/ml_training_snippets.prompt.like
   remove-nodes:x:@.arguments/*/ml_training_snippets.completion.like
   remove-nodes:x:@.arguments/*/ml_training_snippets.uri.like
   add:x:./*/data.read/*/where/*/and
      get-nodes:x:@.arguments/*
   add:x:./*/data.delete/*/where/*
      get-nodes:x:@.arguments/*
   if
      not-exists:x:@data.connect/*/data.read/*/where/*/and/*/or/*
      .lambda
         remove-nodes:x:@data.connect/*/data.read/*/where/*/and/*/or
         remove-nodes:x:@data.connect/*/data.delete/*/where/*/and/*/or
   data.read
      table:ml_training_snippets
      columns
         filename
      where
         and
            filename.neq
            or
      limit:-1
   for-each:x:@data.read/*
      if
         and
            not-null:x:@.dp/#/*/filename
            io.file.exists:x:@.dp/#/*/filename
         .lambda
            io.file.delete:x:@.dp/#/*/filename
   data.read
      generate:true
      table:ml_training_snippets
      columns
         id
            as:rowid
      where
         and
            or
      limit:-1
   .sql
   set-value:x:@.sql
      strings.concat
         .:delete from vss_ml_training_snippets where rowid in(
         get-value:x:@data.read
         .:)
   add:x:./*/data.execute
      get-nodes:x:@data.read/*
   data.execute:x:@.sql
   data.delete
      table:ml_training_snippets
      where
         and
            or
   unwrap:x:+/*
   return
      affected:x:@data.delete
