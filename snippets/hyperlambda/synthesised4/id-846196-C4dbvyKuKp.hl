
// This Hyperlambda code evaluates conditions to execute a webhook signal. It first checks if [outgoing] is not null or empty using [and] and [neq]. If true, it sets up a webhook URL and executes a signal. It then checks if [to] and [from] arguments exist and contain a colon using [if] and [strings.contains]. If true, it splits these values and adds them to the signal. If false, it adds the original [to] and [from] nodes. Finally, it evaluates the constructed execution node with [eval].
if
   and
      not-null:x:@.outgoing
      neq:x:@.outgoing
         .:
   .lambda
      .exe
         .hook-url
         set-value:x:@.hook-url
            get-first-value
               get-value:x:@.arguments/*/webhook_outgoing_url
               config.get:"magic:openai:integrations:outgoing:url"
         unwrap:x:./*/signal/*/url
         signal:x:@.outgoing
            url:x:@.hook-url
      unwrap:x:+/*/*
      add:x:@.exe/*/signal
         .
            result:x:@.result
            session:x:@.arguments/*/session
      if
         and
            exists:x:@.arguments/*/to
            exists:x:@.arguments/*/from
            not-null:x:@.arguments/*/to
            not-null:x:@.arguments/*/from
            strings.contains:x:@.arguments/*/to
               .:":"
            strings.contains:x:@.arguments/*/from
               .:":"
         .lambda
            .channel
            .to
            .from
            strings.split:x:@.arguments/*/to
               .:":"
            set-value:x:@.channel
               get-value:x:@strings.split/0
            set-value:x:@.to
               get-value:x:@strings.split/1
            strings.split:x:@.arguments/*/from
               .:":"
            set-value:x:@.from
               get-value:x:@strings.split/1
            unwrap:x:+/*/*
            add:x:@.exe/*/signal
               .
                  to:x:@.from
                  from:x:@.to
                  channel:x:@.channel
      else
         add:x:@.exe/*/signal
            get-nodes:x:@.arguments/*/to
            get-nodes:x:@.arguments/*/from
      eval:x:@.exe
