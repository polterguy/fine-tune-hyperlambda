
/*
 * // This Hyperlambda code processes a set of strings, performing various operations based on specific conditions and values. It starts by checking if a string does not start with a "#" character, then splits the string by ":" and ensures there are at least two parts. Depending on the first part of the split string, it executes different logic for "Crawl-Delay", "User-Agent", "Sitemap", "Disallow", and "Allow" cases. The code handles values for these cases, including trimming, converting, and validating them, and performs actions like signaling warnings, adding to lists, and setting flags based on conditions.
 * // 1. [if] - Checks if the string does not start with "#"
 * // 2. [strings.starts-with] - Checks if the string starts with a specific character
 * // 3. [strings.split] - Splits the string by ":"
 * // 4. [if] - Checks if there are at least two parts after splitting
 * // 5. [mte] - Ensures there are at least two parts
 * // 6. [set-value] - Sets the value of a node
 * // 7. [strings.join] - Joins parts of the string with ":"
 * // 8. [strings.trim] - Trims whitespace from the string
 * // 9. [switch] - Switches based on the first part of the split string
 * // 10. [case] - Handles specific cases like "Crawl-Delay", "User-Agent", etc.
 * // 11. [try] - Attempts to execute code and catches exceptions
 * // 12. [math.multiply] - Multiplies a value by 1000
 * // 13. [convert] - Converts a value to an integer
 * // 14. [sockets.signal] - Sends a signal with a warning message
 * // 15. [remove-nodes] - Removes nodes from a list
 * // 16. [add] - Adds a value to a list
 * // 17. [strings.concat] - Concatenates strings
 * // 18. [unwrap] - Evaluates expressions
 * // 19. [strings.replace] - Replaces parts of a string
 * // 20. [and] - Logical AND operation
 * // 21. [or] - Logical OR operation
 * // 22. [eq] - Checks for equality
 * // 23. [neq] - Checks for inequality
 * // 24. [mt] - Checks if a value is more than another
 * // 25. [yield] - Returns a value or node list
 */
if
   not
      strings.starts-with:x:@strings.trim
         .:#
   .lambda
      strings.split:x:@strings.trim
         .:":"
      if
         mte
            get-count:x:@strings.split/*
            .:int:2
         .lambda
            .value
            set-value:x:@.value
               strings.join:x:@strings.split/*/[1,100]
                  .:":"
            set-value:x:@strings.split/0
               strings.trim:x:@strings.split/0
                  .:@"
	 "
            set-value:x:@.value
               strings.trim:x:@.value
                  .:@"
	 "
            switch:x:@strings.split/0
               case:Crawl-Delay
               case:Crawl-delay
               case:crawl-delay
                  set-value:x:@.value
                     strings.replace-not-of:x:@.value
                        .:0123456789
                        .:
                  if
                     and
                        eq:x:@.relevant
                           .:bool:true
                        neq:x:@.value
                           .:
                     .lambda
                        try
                           set-value:x:@.crawl-delay
                              math.multiply
                                 .:int:1000
                                 convert:x:@.value
                                    type:int
                           if
                              mt:x:@.crawl-delay
                                 .:int:60000
                              .lambda
                                 sockets.signal:x:@.arguments/*/feedback-channel
                                    args
                                       message:Crawl-Delay was more than 60 seconds, using 60 seconds as our value
                                       type:warning
                                 set-value:x:@.crawl-delay
                                    .:int:60000
                        .catch
                           sockets.signal:x:@.arguments/*/feedback-channel
                              args
                                 message:Crawl-Delay was not a valid integer value, using 10 seconds as our value
                                 type:warning
                           set-value:x:@.crawl-delay
                              .:int:10000
               case:User-Agent
               case:User-agent
               case:user-agent
                  switch:x:@.value
                     case:*
                     case:AINIRO
                     case:GPTBot
                        if
                           eq:x:@.value
                              .:AINIRO
                           .lambda
                              remove-nodes:x:@.allow/*
                              remove-nodes:x:@.disallow/*
                              set-value:x:@.has-seen-ainiro
                                 .:bool:true
                              set-value:x:@.relevant
                                 .:bool:true
                        else
                           if
                              eq:x:@.has-seen-ainiro
                                 .:bool:false
                              .lambda
                                 set-value:x:@.relevant
                                    .:bool:true
                           else
                              set-value:x:@.relevant
                                 .:bool:false
                     default
                        set-value:x:@.relevant
                           .:bool:false
               case:Sitemap
               case:sitemap
                  if
                     or
                        strings.starts-with:x:@.value
                           .:"http://"
                        strings.starts-with:x:@.value
                           .:"https://"
                     .lambda
                        unwrap:x:+/*/*
                        add:x:@.sitemap
                           .
                              .:x:@.value
                  else
                     strings.concat
                        get-value:x:@.domain
                        .:/
                        get-value:x:@.value
                     strings.replace:x:@strings.concat
                        .://
                        .:/
                     unwrap:x:+/*/*
                     add:x:@.sitemap
                        .
                           .:x:@strings.replace
               case:Disallow
               case:disallow
                  if
                     eq:x:@.relevant
                        .:bool:true
                     .lambda
                        if
                           or
                              strings.starts-with:x:@.value
                                 .:"http://"
                              strings.starts-with:x:@.value
                                 .:"https://"
                           .lambda
                              unwrap:x:+/*/*
                              add:x:@.disallow
                                 .
                                    .:x:@.value
                        else
                           strings.concat
                              get-value:x:@.domain
                              get-value:x:@.value
                           unwrap:x:+/*/*
                           add:x:@.disallow
                              .
                                 .:x:@strings.concat
               case:Allow
               case:allow
                  if
                     eq:x:@.relevant
                        .:bool:true
                     .lambda
                        if
                           or
                              strings.starts-with:x:@.value
                                 .:"http://"
                              strings.starts-with:x:@.value
                                 .:"https://"
                           .lambda
                              unwrap:x:+/*/*
                              add:x:@.allow
                                 .
                                    .:x:@.value
                        else
                           strings.concat
                              get-value:x:@.domain
                              get-value:x:@.value
                           unwrap:x:+/*/*
                           add:x:@.allow
                              .
                                 .:x:@strings.concat
