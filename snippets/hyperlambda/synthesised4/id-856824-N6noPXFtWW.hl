
/*
 * Creates a dynamic slot named [magic.db.mssql.columns] that retrieves column metadata from a specified SQL Server database table. It validates the presence of 'database' and 'table' arguments, constructs a connection string, and connects to the database. It executes a SQL query to fetch column details, processes each column's metadata, and maps SQL data types to Hyperlambda types. The slot returns a node list containing the processed column metadata.
 * 
 * 1. [slots.create] - Creates a dynamic slot named [magic.db.mssql.columns].
 * 2. [validators.mandatory] - Ensures 'database' and 'table' arguments are provided.
 * 3. [.connection] - Initializes the connection string.
 * 4. [if] - Checks if a custom connection string is provided.
 * 5. [set-value] - Sets the connection string based on conditions.
 * 6. [mssql.connect] - Connects to the SQL Server database using the connection string.
 * 7. [mssql.select] - Executes a SQL query to retrieve column metadata.
 * 8. [for-each] - Iterates over the result set of the SQL query.
 * 9. [unwrap] - Processes each column's metadata.
 * 10. [.tmp] - Temporarily stores processed column metadata.
 * 11. [if] - Checks if the column has a default value.
 * 12. [switch] - Maps SQL data types to Hyperlambda types.
 * 13. [add] - Adds processed metadata to the result set.
 * 14. [return-nodes] - Returns the processed column metadata as a node list.
 */
slots.create:magic.db.mssql.columns
   validators.mandatory:x:@.arguments/*/database
   validators.mandatory:x:@.arguments/*/table
   .connection:[master]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               get-value:x:@.arguments/*/database
               .:]
   else
      set-value:x:@.connection
         strings.concat
            .:[generic|
            get-value:x:@.arguments/*/database
            .:]
   mssql.connect:x:@.connection
      mssql.select:"select c.name 'name', t.Name 'type', c.is_nullable 'nullable', isnull(i.is_primary_key, 0) 'primary', c.is_identity 'automatic', c.default_object_id 'default'\n         from sys.columns c \n             inner join sys.types t on c.user_type_id = t.user_type_id\n             left outer join sys.index_columns ic \n             left outer join sys.indexes i on ic.object_id = i.object_id and \n                 ic.index_id = i.index_id on ic.object_id = c.object_id and \n                 ic.column_id = c.column_id and i.is_primary_key = 1\n         where c.object_id = OBJECT_ID(@table)"
         @table:x:@.arguments/*/table
      for-each:x:@mssql.select/*
         unwrap:x:+/*/*
         .tmp
            ""
               name:x:@.dp/#/*/name
               db:x:@.dp/#/*/type
               nullable:x:@.dp/#/*/nullable
               primary:x:@.dp/#/*/primary
               automatic:x:@.dp/#/*/automatic
         if
            mt
               get-value:x:@.dp/#/*/default
               .:int:0
            .lambda
               set-value:x:@.tmp/*/*/automatic
                  .:bool:true
         .type
         switch:x:@.dp/#/*/type
            case:date
            case:datetime
            case:datetime2
            case:datetimeoffset
            case:smalldatetime
            case:time
               set-value:x:@.type
                  .:date
            case:numeric
            case:decimal
            case:smallmoney
            case:money
            case:float
            case:real
               set-value:x:@.type
                  .:decimal
            case:bigint
            case:smallint
            case:int
            case:tinyint
               set-value:x:@.type
                  .:long
            case:bit
               set-value:x:@.type
                  .:bool
            case:char
            case:varchar
            case:text
            case:nchar
            case:nvarchar
            case:ntext
            case:binary
            case:image
            case:varbinary
            case:uniqueidentifier
               set-value:x:@.type
                  .:string
            case:rowversion
            case:timestamp
               set-value:x:@.type
                  .:string
               set-value:x:@.tmp/*/*/automatic
                  .:bool:true
            default
               set-value:x:@.type
                  .:string
         unwrap:x:+/*/*
         add:x:@.tmp/*
            .
               hl:x:@.type
         add:x:../*/mssql.connect/*/return-nodes
            get-nodes:x:@.tmp/*
      return-nodes
