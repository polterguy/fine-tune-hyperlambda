
/*
 * // This Hyperlambda code is designed to handle the summarization and management of machine learning training snippets. It checks if the "summarize" argument is true, retrieves configuration data from a database, and iterates over snippets to determine if they need summarization based on their size. If needed, it uses OpenAI's API to generate summaries, updating the snippets accordingly. The code also manages existing snippets in the database, deleting or updating them based on certain conditions, and provides feedback through a feedback channel. Finally, it reports on the import process, indicating how many snippets were imported, already existed, or failed during import.
 * // 1. [else] - Begins an else block for conditional branching.
 * // 2. [if] - Checks if both "summarize" exists and is true.
 * // 3. [and] - Logical AND for combining conditions.
 * // 4. [exists] - Checks if a node exists.
 * // 5. [eq] - Checks equality between two values.
 * // 6. [.lambda] - Executes a block of code if the condition is true.
 * // 7. [.max-size] - Placeholder for maximum size value.
 * // 8. [.api-key] - Placeholder for API key value.
 * // 9. [.model] - Placeholder for model value.
 * // 10. [data.connect] - Connects to a database.
 * // 11. [data.read] - Reads data from a database table.
 * // 12. [table] - Specifies the table to read from.
 * // 13. [columns] - Specifies columns to retrieve.
 * // 14. [where] - Specifies conditions for data retrieval.
 * // 15. [set-value] - Sets a node's value.
 * // 16. [math.multiply] - Multiplies two numbers.
 * // 17. [convert] - Converts a value to a specified type.
 * // 18. [for-each] - Iterates over a set of nodes.
 * // 19. [.tmp] - Temporary storage node.
 * // 20. [strings.concat] - Concatenates strings.
 * // 21. [get-value] - Retrieves a node's value.
 * // 22. [if] - Checks if a condition is true.
 * // 23. [mt] - Checks if one value is more than another.
 * // 24. [openai.tokenize] - Tokenizes text for OpenAI.
 * // 25. [sockets.signal] - Sends a signal to a feedback channel.
 * // 26. [sleep] - Pauses execution for a specified time.
 * // 27. [.token] - Placeholder for token value.
 * // 28. [strings.concat] - Concatenates strings.
 * // 29. [get-first-value] - Retrieves the first value from a set.
 * // 30. [.no] - Counter for retries.
 * // 31. [while] - Loops while a condition is true.
 * // 32. [try] - Attempts to execute code, catching exceptions.
 * // 33. [unwrap] - Evaluates expressions.
 * // 34. [http.post] - Sends an HTTP POST request.
 * // 35. [headers] - Specifies HTTP headers.
 * // 36. [payload] - Specifies HTTP payload.
 * // 37. [messages] - Specifies messages for OpenAI API.
 * // 38. [if] - Checks if a condition is true.
 * // 39. [eq] - Checks equality between two values.
 * // 40. [lambda2hyper] - Converts lambda to Hyperlambda.
 * // 41. [log.error] - Logs an error message.
 * // 42. [else-if] - Checks another condition if previous was false.
 * // 43. [mte] - Checks if one value is more than or equal to another.
 * // 44. [lt] - Checks if one value is less than another.
 * // 45. [strings.trim] - Trims whitespace from a string.
 * // 46. [strings.substring] - Extracts a substring.
 * // 47. [else] - Executes if no conditions are true.
 * // 48. [math.decrement] - Decreases a value by one.
 * // 49. [.catch] - Handles exceptions.
 * // 50. [.below-threshold] - Counter for below threshold snippets.
 * // 51. [.total-snippets] - Total number of snippets.
 * // 52. [get-count] - Counts nodes.
 * // 53. [.already-exists] - Counter for existing snippets.
 * // 54. [.failed] - Counter for failed imports.
 * // 55. [data.connect] - Connects to a database.
 * // 56. [data.read] - Reads data from a database table.
 * // 57. [if] - Checks if a condition is true.
 * // 58. [not-exists] - Checks if a node does not exist.
 * // 59. [throw] - Throws an exception.
 * // 60. [data.execute] - Executes a database command.
 * // 61. [data.delete] - Deletes data from a database table.
 * // 62. [.similar] - Counter for similar snippets.
 * // 63. [for-each] - Iterates over a set of nodes.
 * // 64. [if] - Checks if a condition is true.
 * // 65. [and] - Logical AND for combining conditions.
 * // 66. [not-null] - Checks if a node is not null.
 * // 67. [mt] - Checks if one value is more than another.
 * // 68. [strings.length] - Gets the length of a string.
 * // 69. [add] - Adds nodes together.
 * // 70. [try-signal] - Tries to send a signal.
 * // 71. [strings.concat] - Concatenates strings.
 * // 72. [data.read] - Reads data from a database table.
 * // 73. [if] - Checks if a condition is true.
 * // 74. [not-exists] - Checks if a node does not exist.
 * // 75. [data.create] - Creates data in a database table.
 * // 76. [else-if] - Checks another condition if previous was false.
 * // 77. [eq] - Checks equality between two values.
 * // 78. [math.increment] - Increases a value by one.
 * // 79. [data.update] - Updates data in a database table.
 * // 80. [else] - Executes if no conditions are true.
 * // 81. [if] - Checks if a condition is true.
 * // 82. [neq] - Checks if two values are not equal.
 * // 83. [strings.concat] - Concatenates strings.
 * // 84. [.imported] - Counter for imported snippets.
 * // 85. [math.subtract] - Subtracts one value from another.
 * // 86. [math.add] - Adds nodes together.
 * // 87. [if] - Checks if a condition is true.
 * // 88. [mt] - Checks if one value is more than another.
 * // 89. [strings.concat] - Concatenates strings.
 * // 90. [else] - Executes if no conditions are true.
 */
else
   if
      and
         exists:x:@.arguments/*/summarize
         eq:x:@.arguments/*/summarize
            .:bool:true
      .lambda
         .max-size
         .api-key
         .model
         data.connect:[generic|magic]
            data.read
               table:ml_types
               columns
                  max_context_tokens
                  api_key
                  model
               where
                  and
                     type.eq:x:@.arguments/*/type
            set-value:x:@.max-size
               math.multiply
                  convert:x:@data.read/*/*/max_context_tokens
                     type:decimal
                  .:decimal:0.8
            set-value:x:@.max-size
               convert:x:@.max-size
                  type:int
            set-value:x:@.api-key
               get-value:x:@data.read/*/*/api_key
            set-value:x:@.model
               get-value:x:@data.read/*/*/model
         for-each:x:@.snippets/*
            .tmp
            set-value:x:@.tmp
               strings.concat
                  get-value:x:@.dp/#/*/prompt
                  .:@"
"
                  .:@"
"
                  get-value:x:@.dp/#/*/completion
            if
               mt
                  openai.tokenize:x:@.tmp
                  get-value:x:@.max-size
               .lambda
                  sockets.signal:x:@.arguments/*/feedback-channel
                     args
                        message:Summarizing snippet since it is larger than 80% of maximum context size for type
                        type:info
                  sleep:100
                  .token
                  set-value:x:@.token
                     strings.concat
                        .:"Bearer "
                        get-first-value
                           get-value:x:@.api-key
                           config.get:"magic:openai:key"
                  .no:int:3
                  while
                     mt:x:@.no
                        .:int:0
                     .lambda
                        try
                           unwrap:x:+/**
                           http.post:"https://api.openai.com/v1/chat/completions"
                              headers
                                 Authorization:x:@.token
                                 Content-Type:application/json
                              payload
                                 model:x:@.model
                                 max_tokens:x:@.max-size
                                 temperature:decimal:0.3
                                 messages
                                    .
                                       role:system
                                       content:Create a summary of the following information
                                    .
                                       role:user
                                       content:x:@.dp/#/*/completion
                              convert:true
                           if
                              eq:x:@http.post
                                 .:int:400
                              .lambda
                                 sockets.signal:x:@.arguments/*/feedback-channel
                                    args
                                       message:Could not summarize snippet, check your log for details
                                       type:warning
                                 sleep:100
                                 lambda2hyper:x:@http.post
                                 log.error:Something went wrong while invoking OpenAI
                                    message:x:@http.post/*/content/*/error/*/message
                                    status:x:@http.post
                                    error:x:@lambda2hyper
                                 set-value:x:@.no
                                    .:int:0
                           else-if
                              and
                                 mte:x:@http.post
                                    .:int:200
                                 lt:x:@http.post
                                    .:int:300
                              .lambda
                                 sockets.signal:x:@.arguments/*/feedback-channel
                                    args
                                       message:Successfully created summary of snippet
                                       type:info
                                 sleep:100
                                 set-value:x:@.dp/#/*/completion
                                    strings.trim:x:@http.post/*/content/*/choices/0/*/message/*/content
                                       .:@"
	 "
                                 set-value:x:@.dp/#/*/prompt
                                    strings.concat
                                       .:"Summary; "
                                       strings.substring:x:@.dp/#/*/prompt
                                          .:int:0
                                          .:int:150
                                 set-value:x:@.no
                                    .:int:0
                           else
                              sockets.signal:x:@.arguments/*/feedback-channel
                                 args
                                    message:Could not summarize snippet, check your log for details. Trying again in 3 seconds.
                                    type:warning
                              sleep:3000
                              lambda2hyper:x:@http.post
                              log.error:Something went wrong while invoking OpenAI
                                 message:x:@http.post/*/content/*/error/*/message
                                 status:x:@http.post
                                 error:x:@lambda2hyper
                              math.decrement:x:@.no
                        .catch
                           math.decrement:x:@.no
   .below-threshold:int:0
   .total-snippets
   set-value:x:@.total-snippets
      get-count:x:@.snippets/*
   .already-exists:int:0
   .failed:int:0
   data.connect:[generic|magic]
      data.read
         table:ml_types
         columns
            id
         where
            and
               type.eq:x:@.arguments/*/type
      if
         not-exists:x:@data.read/*
         .lambda
            throw:Machine learning type does not exist.
               type:x:@.arguments/*/type
      data.read
         table:ml_training_snippets
         columns
            count(*)
               as:count
         where
            and
               type.eq:x:@.arguments/*/type
               uri.eq:x:@.arguments/*/url
               meta.eq:AINIRO-Website-Crawler
      if
         neq
            convert:x:@data.read/*/*/count
               type:int
            .:int:0
         .lambda
            strings.concat
               .:"Deleting "
               get-value:x:@data.read/*/*/count
               .:" old training snippets with the same URL"
            unwrap:x:+/**
            sockets.signal:x:@.arguments/*/feedback-channel
               args
                  message:x:@strings.concat
                  type:info
            sleep:10
      data.execute:@"
   delete from vss_ml_training_snippets
      where rowid in (select id as rowid from ml_training_snippets where type = @type and uri = @url)"
         @type:x:@.arguments/*/type
         @url:x:@.arguments/*/url
      data.delete
         table:ml_training_snippets
         where
            and
               type.eq:x:@.arguments/*/type
               uri.eq:x:@.arguments/*/url
               meta.eq:AINIRO-Website-Crawler
      .similar:int:0
      for-each:x:@.snippets/*
         if
            and
               exists:x:@.dp/#/*/completion
               not-null:x:@.dp/#/*/completion
            .lambda
               if
                  mt
                     strings.length:x:@.dp/#/*/completion
                     get-value:x:@.arguments/*/threshold
                  .lambda
                     add:x:+
                        get-nodes:x:@.arguments/*/type
                     try-signal:magic.ai.can-create-snippet
                     strings.concat
                        get-value:x:@.dp/#/*/completion
                        .:@"
"
                        .:@"
"
                        .:This information was taken from [this URL](
                        .:%
                     data.read
                        table:ml_training_snippets
                        columns
                           id
                           prompt
                           completion
                           uri
                        where
                           and
                              type.eq:x:@.arguments/*/type
                              or
                                 completion.eq:x:@.dp/#/*/completion
                                 completion.like:x:@strings.concat
                     if
                        not-exists:x:@data.read/*
                        .lambda
                           if:x:@.arguments/*/insert_url
                              set-value:x:@.dp/#/*/completion
                                 strings.concat
                                    get-value:x:@.dp/#/*/completion
                                    .:@"
"
                                    .:@"
"
                                    .:This information was taken from [this URL](
                                    get-value:x:@.arguments/*/url
                                    .:)
                           data.create
                              table:ml_training_snippets
                              values
                                 type:x:@.arguments/*/type
                                 uri:x:@.arguments/*/url
                                 prompt:x:@.dp/#/*/prompt
                                 completion:x:@.dp/#/*/completion
                                 meta:AINIRO-Website-Crawler
                     else-if
                        eq:x:@data.read/0/*/prompt
                           .:Common
                        .lambda
                           math.increment:x:@.already-exists
                     else-if
                        not
                           eq:x:@data.read/*/*/prompt
                              get-value:x:@.dp/#/*/prompt
                        .lambda
                           math.increment:x:@.similar
                           data.update
                              table:ml_training_snippets
                              values
                                 prompt:Common
                              where
                                 and
                                    id.eq:x:@data.read/0/*/id
               else
                  math.increment:x:@.below-threshold
      if
         neq:x:@.similar
            .:int:0
         .lambda
            strings.concat
               get-value:x:@.similar
               .:" snippets with similar completion exists, updating prompt to 'Common'"
            unwrap:x:+/**
            sockets.signal:x:@.arguments/*/feedback-channel
               args
                  message:x:@strings.concat
                  type:info
            sleep:10
   .imported
   set-value:x:@.imported
      math.subtract:x:@.total-snippets
         math.add
            get-value:x:@.below-threshold
            get-value:x:@.already-exists
   if
      mt:x:@.below-threshold
         .:int:0
      .lambda
         strings.concat
            get-value:x:@.below-threshold
            .:" snippets was below threshold and hence was not imported"
         unwrap:x:+/**
         sockets.signal:x:@.arguments/*/feedback-channel
            args
               message:x:@strings.concat
               type:info
         sleep:100
   if
      mt:x:@.already-exists
         .:int:0
      .lambda
         strings.concat
            get-value:x:@.already-exists
            .:" snippets already exists in database and hence was not imported"
         unwrap:x:+/**
         sockets.signal:x:@.arguments/*/feedback-channel
            args
               message:x:@strings.concat
               type:info
         sleep:100
   if
      mt:x:@.failed
         .:int:0
      .lambda
         strings.concat
            get-value:x:@.failed
            .:" snippets failed during import, probably because of OpenAI not being able to summarize them"
         unwrap:x:+/**
         sockets.signal:x:@.arguments/*/feedback-channel
            args
               message:x:@strings.concat
               type:warning
         sleep:100
   if
      mt:x:@.imported
         .:int:0
      .lambda
         strings.concat
            .:"Done importing or updating "
            get-value:x:@.imported
            .:" training snippets from page"
         unwrap:x:+/**
         sockets.signal:x:@.arguments/*/feedback-channel
            args
               message:x:@strings.concat
               type:info
         sleep:100
   else
      sockets.signal:x:@.arguments/*/feedback-channel
         args
            message:We were not able to successfully import any snippets from page!
            type:warning
      sleep:100
