
// This Hyperlambda code defines a dynamic slot [magic.ai.html.extract] that processes HTML input to extract structured data. It validates mandatory arguments [html] and [url], ensuring [url] is valid, and sets defaults for [images], [lists], and [code]. The slot converts HTML to Markdown using [html2markdown], extracts titles and descriptions, and processes headers and lists to create snippets. It returns a structured response with [urls], [snippets], and [meta] data.
slots.create:magic.ai.html.extract
   validators.mandatory:x:@.arguments/*/html
   validators.mandatory:x:@.arguments/*/url
   validators.url:x:@.arguments/*/url
   validators.default:x:@.arguments
      images:bool:true
      lists:bool:true
      code:bool:true
   .meta
      main:int:0
   .snippets
   .title
   .description
   .markdown
   .urls
   html2markdown:x:@.arguments/*/html
      url:x:@.arguments/*/url
      images:x:@.arguments/*/images
      lists:x:@.arguments/*/lists
      code:x:@.arguments/*/code
   if
      or
         null:x:@html2markdown
         eq:x:@html2markdown
            .:
      .lambda
         return
            urls
            snippets
            meta
               main:int:0
   set-value:x:@.title
      get-value:x:@html2markdown/*/title/[0,1]
   set-value:x:@.description
      get-value:x:@html2markdown/*/description/[0,1]
   add:x:@.urls
      get-nodes:x:@html2markdown/*/urls/*
   set-value:x:@.markdown
      get-value:x:@html2markdown
   .prompt
   set-value:x:@.prompt
      get-first-value
         get-value:x:@.title
         get-value:x:@.description
         .:Page
   .remaining
   .tmp-prompt
   set-value:x:@.tmp-prompt
      get-value:x:@.prompt
   markdown2html:x:@html2markdown
   html2lambda:x:@markdown2html
   for-each:x:@html2lambda/*
      get-name:x:@.dp/#
      switch:x:@get-name
         case:h1
         case:h2
         case:h3
         case:h4
         case:h5
         case:h6
            set-value:x:@.tmp-prompt
               strings.concat
                  get-value:x:@.prompt
                  .:" | "
                  get-value:x:"@.dp/#/*/\\#text"
         case:ul
         case:ol
         case:pre
            lambda2html:x:@.dp/#
            html2markdown:x:@lambda2html
               url:x:@.arguments/*/url
            unwrap:x:+/*/*/*
            add:x:@.snippets
               .
                  .
                     prompt:x:@.tmp-prompt
                     completion:x:@html2markdown
         default
            add:x:@.remaining
               get-nodes:x:@.dp/#
   lambda2html:x:@.remaining/*
   set-value:x:@.markdown
      html2markdown:x:@lambda2html
         url:x:@.arguments/*/url
   .completion
   set-value:x:@.completion
      get-value:x:@.markdown
   unwrap:x:+/*/*/*
   add:x:@.snippets
      .
         .
            prompt:x:@.prompt
            completion:x:@.completion
   add:x:./*/return/*/snippets
      get-nodes:x:@.snippets/*
   add:x:./*/return/*/meta
      get-nodes:x:@.meta/*
   add:x:./*/return/*/urls
      get-nodes:x:@.urls/*
   return
      urls
      snippets
      meta
