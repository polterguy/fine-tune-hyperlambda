
// Endpoint to remove the specified [roles] from the specified [username], where [roles] is supplied as a list of strings.
.arguments
   username:string
   roles:*

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/username
validators.mandatory:x:@.arguments/*/roles

// Connecting to database.
data.connect:[generic|magic]

   // Verifying user exists.
   data.read
      table:users
      columns
         username
      where
         and
            username.eq:x:@.arguments/*/username

   // Verifying above invocation returned something.
   if
      not-exists:x:@data.read/*
      .lambda

         // Oops ...!!
         throw:No such user
            username:x:@.arguments/*/username

   // Removing [roles] from [username]
   for-each:x:@.arguments/*/roles/*
      data.delete
         table:users_roles
         where
            and
               user.eq:x:@.arguments/*/username
               role.eq:x:@.dp/#
