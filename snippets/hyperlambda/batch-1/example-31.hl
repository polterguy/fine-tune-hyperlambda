/*
 * Initializes the Chinook SQLite database if it doesn't already exist by migrating `.sql` scripts
 * from the startup module and executing the schema setup file.
 *
 * 1. **Script Migration**:
 *    - Uses [io.file.list] to retrieve `.sql` files from the module's startup directory.
 *    - For each `.sql` file, checks if it already exists in the target template path (/etc/sqlite/templates/).
 *    - If not found, it moves the script file using [io.file.move] and logs the migration event.
 *
 * 2. **Database Initialization**:
 *    - Checks whether `/data/chinook.db` exists using [io.file.exists].
 *    - If not, creates an empty database file with [io.file.save], logs the creation,
 *      loads the main SQL setup script [chinook.sql] using [io.file.load], and executes
 *      its contents using [sqlite.execute] to build the schema.
 *
 * 3. **Cache Invalidation**:
 *    - After creating and initializing the database, it clears the related SQLite database
 *      cache entry to ensure the environment reflects the changes immediately.
 *
 * This setup process allows first-time deployment of the Chinook schema automatically,
 * enabling on-demand database provisioning and migration workflows.
 */
io.file.list:/modules/sqlite-chinook/magic.startup/

// Looping through the result of above invocation
for-each:x:-/*
   strings.split:x:@.dp/#
      .:/

   // Making sure this is .sql file
   if
      strings.ends-with:x:@.dp/#
         .:.sql
      .lambda
         strings.concat
            .:/etc/sqlite/templates/
            get-value:x:@strings.split/0/-
         if
            not
               io.file.exists:x:@strings.concat
            .lambda

               // File doesn't exists, hence moving it.
               io.file.move:x:@.dp/#
                  .:/etc/sqlite/templates/
               log.info:SQL script moved into etc folder
                  filename:x:@.dp/#

// Creates the chinook database file.
if
   not
      io.file.exists:/data/chinook.db
   .lambda

      // Creating a default database file to store database in.
      io.file.save:/data/chinook.db
         .:
      log.info:Chinook database file created
         filename:/data/chinook.db

      // Loads and executes the chinook.sql script.
      io.file.load:/etc/sqlite/templates/chinook.sql
      sqlite.connect:chinook
         sqlite.execute:x:@io.file.load
      log.info:The chinook database was successfully created
         filename:/etc/sqlite/templates/chinook.sql

      // Evicting cache item related to SQLite.
      cache.set:magic.sql.databases.sqlite.generic
