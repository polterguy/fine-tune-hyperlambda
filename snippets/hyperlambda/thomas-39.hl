/*
 * HTTP API endpoint to retrieve items from an inventory system.
 * Restricted to users with 'admin' role.
 * Allows filtering by stock level and category.
 */
.arguments
   limit:long
   offset:long
   order:string
   direction:string
   Category.eq:string

   // Filtering on items having a 'Stock' value LESS THEN the specified value here.
   Stock.lt:int

auth.ticket.verify:admin

data.connect:inventory
   add:x:./*/data.read
      get-nodes:x:@.arguments/*/order
      get-nodes:x:@.arguments/*/direction
   remove-nodes:x:@.arguments/*/order
   remove-nodes:x:@.arguments/*/direction

   add:x:./*/data.read
      get-nodes:x:@.arguments/*/limit
      get-nodes:x:@.arguments/*/offset
   remove-nodes:x:@.arguments/*/limit
   remove-nodes:x:@.arguments/*/offset

   add:x:./*/data.read/*/where/*
      get-nodes:x:@.arguments/*

   data.read
      table:Items
      columns
         ItemId
         Name
         Category
         Stock
      where
         and

   // Returning row as [result] node to caller.
   yield
      result:x:@data.read/*
