
/*
 * Returns the specified CSS file to caller from '/etc/system/openai/css/search/'.
 *
 * This code will first check if there exists a custom file in '/etc/system/openai/css/search/' with
 * the specified name, and if not, look in '/system/openai/front.files/search/'.
 * If it doesn't find the file in either folder, it will throw an exception.
 */
.arguments
   file:string

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file

// Checking if custom CSS file exists.
.result
strings.concat
   .:/etc/system/openai/css/search/
   get-value:x:@.arguments/*/file
   .:.css
if
   io.file.exists:x:@strings.concat
   .lambda

      // Custom template file.
      set-value:x:@.result
         io.file.load:x:@strings.concat

else

   //Checking if system CSS file exists.
   strings.concat
      .:/system/openai/front.files/search/
      get-value:x:@.arguments/*/file
      .:.css
   if
      io.file.exists:x:@strings.concat
      .lambda

         // Generic template file.
         set-value:x:@.result
            io.file.load:x:@strings.concat

   else

      // Oops, no such file exists.
      throw:No such AI chat template file exists
         public:bool:true
         status:400

// Making sure we return correct Content-Type to caller, and applying some cache.
response.headers.set
   Content-Type:text/css

// Returning result to caller.
return:x:@.result
