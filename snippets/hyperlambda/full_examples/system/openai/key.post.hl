
/*
 * Create an endpoint that allows a root user to configure the OpenAI API integration
 * by setting a new API key into the system configuration.
 *
 * The endpoint performs the following:
 * - Ensures the user is authenticated with root access.
 * - Validates that the provided key is a valid string between 20 and 500 characters.
 * - Loads the current configuration, removes any previously configured OpenAI key,
 *   and sets the new key under [magic:openai:key].
 * - Saves the updated configuration and returns a success response to the caller.
 */
.arguments
   key:string

// Making sure user has access to invoked endpoint.
auth.ticket.verify:root

// Sanity checking invocation
validators.mandatory:x:@.arguments/*/key
validators.string:x:@.arguments/*/key
   min:20
   max:500

// Loading configuration.
config.load
json2lambda:x:@config.load

// Removing existing API key.
remove-nodes:x:@json2lambda/*/magic/*/openai

// Adding specified key to configuration.
unwrap:x:+/*/*/*
add:x:@json2lambda/*/magic
   .
      openai
         key:x:@.arguments/*/key

// Transforming to JSON and saving configuration.
lambda2json:x:@json2lambda/*
config.save:x:@lambda2json

// Returning success to caller.
return
   result:success
