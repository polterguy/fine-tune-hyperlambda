/*
 * Creates a new machine learning model configuration in the [ml_types] table of the "magic" database.
 *
 * This secured endpoint allows root users to register a new ML model type with custom settings, limits,
 * behavior, and integrations. It also auto-generates a default training snippet to seed the new model with
 * introductory content for use in OpenAI fine-tuning or contextual retrieval.
 *
 * INPUT PARAMETERS:
 * - Required:
 *   - [type:string]: Unique identifier for the new model type.
 *   - [model:string]: Base model name (e.g. gpt-4o, gpt-3.5-turbo).
 *   - [max_tokens:long]: Maximum tokens per response.
 *   - [max_context_tokens:long]: Maximum context tokens per request.
 *   - [max_request_tokens:long]: Maximum allowable tokens in an input request.
 *   - [temperature:double]: Randomness level (0.0–1.0) for AI output generation.
 * - Optional (with defaults or extended behavior):
 *   - [vector_model:string] (default: text-embedding-ada-002)
 *   - [threshold:decimal] (default: 0.3)
 *   - [recaptcha:decimal], [auth:string], [supervised:int], [cached:int], [prefix:string], [use_embeddings:int]
 *   - [greeting, base_url, contact_us, lead_email, api_key, system_message, initial_questionnaire]
 *   - Twilio: [twilio_account_sid, twilio_account_id] — defaults to [type] if not provided
 *   - Webhooks: [webhook_incoming, webhook_outgoing, webhook_incoming_url, webhook_outgoing_url]
 *   - Limits: [no_requests:int], [max_requests:int], [max_function_invocations:int], [max_session_items:int]
 *   - Other: [search_postfix, completion_slot, conversation_starters]
 *
 * EXECUTION FLOW:
 *
 * 1. **Authorization & Validation**:
 *    - Verifies the user has root privileges using [auth.ticket.verify:root].
 *    - Ensures required fields are provided and within valid ranges using [validators.mandatory] and [validators.integer].
 *    - Applies sane default values to optional fields where appropriate.
 *
 * 2. **Database Connection & Rule Enforcement**:
 *    - Connects to the "magic" database using [data.connect:[generic|magic]].
 *    - Signals [magic.ai.can-create-machine-learning-model] to check quota, licensing, or permission constraints.
 *
 * 3. **Twilio Fallback Logic**:
 *    - If [twilio_account_id] is missing or empty, it is automatically set to the value of [type].
 *
 * 4. **Record Insertion**:
 *    - Inserts the new model record into the [ml_types] table using [data.create].
 *
 * 5. **Default Training Snippet Injection**:
 *    - Immediately adds a default [ml_training_snippets] record to seed the model with a basic Q&A pair.
 *    - This provides new models with at least one searchable/fine-tunable entry.
 *
 * 6. **Response**:
 *    - Sets HTTP response status to 201 (Created).
 *    - Returns `{ result: success }` to confirm the operation was completed successfully.
 *
 * This endpoint is essential for system administrators creating new AI chatbot configurations
 * and serves as the foundation for onboarding new OpenAI fine-tuning types with full backend control.
 */

.arguments
   type:string
   model:string
   max_tokens:long
   max_context_tokens:long
   max_request_tokens:long
   temperature:double
   recaptcha:decimal
   auth:string
   supervised:int
   cached:int
   prefix:string
   use_embeddings:int
   threshold:decimal
   vector_model:string
   greeting:string
   base_url:string
   contact_us:string
   lead_email:string
   api_key:string
   twilio_account_id:string
   twilio_account_sid:string
   webhook_incoming:string
   webhook_outgoing:string
   webhook_incoming_url:string
   webhook_outgoing_url:string
   system_message:string
   conversation_starters:string
   initial_questionnaire:string
   no_requests:int
   max_requests:int
   search_postfix:string
   max_function_invocations:int
   max_session_items:int
   completion_slot:string

// Verifying user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.integer:x:@.arguments/*/max_context_tokens
   min:500
   max:40000
validators.integer:x:@.arguments/*/max_request_tokens
   min:100
   max:25000
validators.integer:x:@.arguments/*/max_tokens
   min:500
   max:4000

// Applying sane defaults.
validators.default:x:@.arguments
   vector_model:text-embedding-ada-002
   model:gpt-4o
   threshold:decimal:0.3

// Opening up database connection.
data.connect:[generic|magic]

   // Verifying user can create a-(nother) machine learning model.
   try-signal:magic.ai.can-create-machine-learning-model

   // Parametrising our create invocation.
   add:x:./*/data.create/[0,1]/*/values
      get-nodes:x:@.arguments/*

   // Making sure we default twilio_account_id token to model name unless explicitly specified.
   if
      or
         not-exists:x:@.arguments/*/twilio_account_id
         null:x:@.arguments/*/twilio_account_id
         eq:x:@.arguments/*/twilio_account_id
            .:
      .lambda
         remove-nodes:x:@data.connect/*/data.create/*/values/*/twilio_account_id
         unwrap:x:+/*/*
         add:x:@data.connect/*/data.create/[0,1]/*/values
            .
               twilio_account_id:x:@.arguments/*/type

   // Creating our record.
   data.create
      table:ml_types
      values

   // Creating our default training snippets.
   data.create
      table:ml_training_snippets
      values
         type:x:@.arguments/*/type
         prompt:Who created this AI chatbot?
         completion:@"This chatbot is a custom AI chatbot powered by OpenAI allowing you to use natural language to ask questions related to the website you're currently visiting. It was created by [AINIRO.IO](https://ainiro.io). AINIRO.io have AI chatbot solutions allowing you to scrape your website, upload documents, and create publicly available AI chatbots and AI-based search similar to Bing, in addition to tools helping you build custom GPTs."
         uri:"https://ainiro.io"

   // Returning the correct status code.
   response.status.set:201

   // Returning result of above invocation to caller.
   yield
      result:success
