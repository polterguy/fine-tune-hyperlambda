/*
 * Initializes the backend system at startup by setting up the Magic database and applying default configurations.
 *
 * 1. **Database Initialization Support**: Loads platform-specific startup scripts for each supported database type
 *    (MSSQL, MySQL, PostgreSQL, SQLite). These scripts provide internal helpers used to check and configure
 *    the database engine.
 *
 * 2. **Database Creation**: Executes the universal `ensure-magic-database.hl` script to create the core Magic
 *    database structure, if it does not already exist.
 *
 * 3. **Auto-executing Startup Files**: Recursively lists all files under `/system/` and `/modules/` folders.
 *    It identifies and executes all files containing the `/magic.startup/` path segment and having a `.hl`
 *    file extension.
 *
 * 4. **Execution Logging and Safety**: For each identified startup file:
 *    - Logs the filename to the console for visibility.
 *    - Attempts to execute the file.
 *    - Logs an error if execution fails, capturing both the file name and exception message for diagnostics.
 *
 * This startup routine ensures that all necessary configuration, database setup, and extension modules
 * are applied automatically during application boot, providing a consistent and extensible initialization process.
 */



// Before we can check if magic db exists, we'll need the slots that does the heavy lifting for us.
io.file.execute:/system/sql/magic.startup/mssql/magic.db.mssql.databases.hl
io.file.execute:/system/sql/magic.startup/mysql/magic.db.mysql.databases.hl
io.file.execute:/system/sql/magic.startup/pgsql/magic.db.pgsql.databases.hl
io.file.execute:/system/sql/magic.startup/sqlite/magic.db.sqlite.databases.hl

// Creating our magic database.
io.file.execute:/system/ensure-magic-database.hl

// Executing all magic.startup Hyperlambda files.
io.file.list-recursively:/system/
io.file.list-recursively:/modules/
for-each:x:./*/io.file.list-recursively/*

   // Checking if file is a startup Hyperlambda file.
   if
      and
         strings.contains:x:@.dp/#
            .:/magic.startup/
         strings.ends-with:x:@.dp/#
            .:.hl
      .lambda

         // Executing file.
         strings.concat
            .:"Executing startup file '"
            get-value:x:@.dp/#
            .:"."
         console.log:x:-
         try
            io.file.execute:x:@.dp/#
         .catch
            log.error:Error while executing startup file
               exception:x:@.arguments/*/message
               file:x:@.dp/#
