
/*
 * Create an HTTP endpoint that returns the default database type configured for CRUD operations.
 *
 * ### Behavior:
 * 1. Ensures only root-authenticated users can invoke the endpoint.
 * 2. Retrieves the `magic:databases` section from the system configuration.
 * 3. Returns the default database type configured under `magic:databases.default`.
 * 4. Also returns a list of all configured database types (excluding the `default` key itself),
 *    under the [options] node to help with selection in frontends.
 */

.arguments

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Retrieving database section from configuration.
config.section:"magic:databases"

// Adding default database type to return value.
add:x:../*/return
   get-nodes:x:@config.section/*/default

/*
 * Iterating through each optional database type,
 * and making sure we return those too, making sure
 * we remove 'default' parts before iterating.
 */
remove-nodes:x:@config.section/*/default
for-each:x:@config.section/*
   set-value:x:+/*/*
      get-name:x:@.dp/#
   add:x:../*/return/*/options
      .
         .

// Returning result of above to caller.
return
   options
