/*
 * Dynamically generates and optionally executes a SQL DDL statement to create a many-to-many mapping table
 * between two existing tables in a relational database.
 *
 * The endpoint begins by securing access using [auth.ticket.verify] and performing input validation to ensure
 * all required arguments are provided and conform to expected formats. It supports MySQL, SQLite, PostgreSQL, and MSSQL.
 *
 * The SQL DDL is constructed dynamically by:
 * - Looping over the primary keys of both [table1] and [table2], inserting columns into a new join table.
 * - Appending foreign key constraints if the target database is not MySQL, or preparing them separately for MySQL.
 * - Building a composite primary key using all provided columns from both tables.
 *
 * If the [generate] flag is set to true, it returns the generated SQL string to the client without executing it.
 * Otherwise, it establishes a connection to the target database using [data.connect], and executes the generated SQL
 * to physically create the table in the database.
 *
 * After execution, it clears the relevant entry in the database connection cache to reflect schema changes immediately.
 * Finally, it returns both the SQL used and a success result to the client.
 */
.arguments
   databaseType:string
   connectionString:string
   databaseName:string
   args
      name:string
      table1:string
      table2:string
      table1pk
         .
            name:string
            type:string
            length:int
      table2pk
         .
            name:string
            type:string
            length:int
   generate:bool

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/connectionString
validators.mandatory:x:@.arguments/*/databaseName
validators.mandatory:x:@.arguments/*/args
validators.mandatory:x:@.arguments/*/databaseType
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:sqlite
   .:pgsql
   .:mssql

// Creating our DDL SQL.
.sql

// Create table DDL.
set-value:x:@.sql
   strings.concat
      .:"create table "
      get-value:x:@.arguments/*/args/*/name
      .:"("

// Looping through primary keys in table1.
.first:bool:true
.pk:
.fk:
for-each:x:@.arguments/*/args/*/table1pk/*

   // Checking if this is our first column or not.
   if:x:@.first
      set-value:x:@.first
         .:bool:false
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:"\r\n  "
   else
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:",\r\n  "

   // Adding column name and type.
   set-value:x:@.sql
      strings.concat
         get-value:x:@.sql
         get-value:x:@.dp/#/*/name
         .:" "
         get-value:x:@.dp/#/*/type

   // Checking if type has a length.
   if
      and
         exists:x:@.dp/#/*/length
         not-null:x:@.dp/#/*/length
      .lambda
         set-value:x:@.sql
            strings.concat
               get-value:x:@.sql
               .:(
               get-value:x:@.dp/#/*/length
               .:)

   // Adding references constraint for column if not MySQL.
   if
      neq:x:@.arguments/*/databaseType
         .:mysql
      .lambda
         set-value:x:@.sql
            strings.concat
               get-value:x:@.sql
               .:" references "
               get-value:x:@.arguments/*/args/*/table1
               .:" ("
               get-value:x:@.dp/#/*/name
               .:")"
   else
      set-value:x:@.fk
         strings.concat
            get-value:x:@.fk
            .:",\r\n foreign key ("
            get-value:x:@.dp/#/*/name
            .:") references "
            get-value:x:@.arguments/*/args/*/table1
            .:" ("
            get-value:x:@.dp/#/*/name
            .:)

   // Storing primary key's name.
   set-value:x:@.pk
      strings.concat
         get-value:x:@.pk
         get-value:x:@.dp/#/*/name
         .:,

// Looping through primary keys in table2.
for-each:x:@.arguments/*/args/*/table2pk/*

   // Checking if this is our first column or not.
   if:x:@.first
      set-value:x:@.first
         .:bool:false
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:"\r\n  "
   else
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:",\r\n  "

   // Adding column name and type.
   set-value:x:@.sql
      strings.concat
         get-value:x:@.sql
         get-value:x:@.dp/#/*/name
         .:" "
         get-value:x:@.dp/#/*/type

   // Checking if type has a length.
   if
      and
         exists:x:@.dp/#/*/length
         not-null:x:@.dp/#/*/length
      .lambda
         set-value:x:@.sql
            strings.concat
               get-value:x:@.sql
               .:(
               get-value:x:@.dp/#/*/length
               .:)

   // Adding references constraint for column if not MySQL.
   if
      neq:x:@.arguments/*/databaseType
         .:mysql
      .lambda
         set-value:x:@.sql
            strings.concat
               get-value:x:@.sql
               .:" references "
               get-value:x:@.arguments/*/args/*/table2
               .:" ("
               get-value:x:@.dp/#/*/name
               .:")"
   else
      set-value:x:@.fk
         strings.concat
            get-value:x:@.fk
            .:",\r\n foreign key ("
            get-value:x:@.dp/#/*/name
            .:") references "
            get-value:x:@.arguments/*/args/*/table2
            .:" ("
            get-value:x:@.dp/#/*/name
            .:)

   // Storing primary key's name.
   set-value:x:@.pk
      strings.concat
         get-value:x:@.pk
         get-value:x:@.dp/#/*/name
         .:,

// Adding primary key to table.
strings.trim:x:@.pk
   .:,

set-value:x:@.sql
   strings.concat
      get-value:x:@.sql
      .:",\r\n  primary key("
      get-value:x:@strings.trim
      .:)

// Adding closing paranthesis and semi colon.
set-value:x:@.sql
   strings.concat
      get-value:x:@.sql
      get-value:x:@.fk
      .:");"

// Checking if caller only wants to generate SQL statement.
if:x:@.arguments/*/generate
   unwrap:x:+/*
   return
      sql:x:@.sql

// Creating our database connection and executing our SQL.
strings.concat
   .:[
   get-value:x:@.arguments/*/connectionString
   .:|
   get-value:x:@.arguments/*/databaseName
   .:]
data.connect:x:-
   database-type:x:@.arguments/*/databaseType
   data.execute:x:@.sql
      database-type:x:@.arguments/*/databaseType

// Flushing database cache.
strings.concat
   .:magic.sql.databases.
   get-value:x:@.arguments/*/databaseType
   :.
   get-value:x:@.arguments/*/connectionString
cache.set:x:-

// Returning result to caller.
unwrap:x:+/*
return
   result:success
   sql:x:@.sql

