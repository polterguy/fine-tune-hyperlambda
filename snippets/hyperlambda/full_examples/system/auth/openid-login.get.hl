/*
 * Verifies an OpenID Connect authentication token and handles both returning users
 * and first-time logins securely.
 *
 * Starts by validating the token using [auth.token.verify]. This step will throw an
 * exception if the token is invalid. Then ensures the 'nonce' in the token is valid and
 * not reused by checking it against cached valuesâ€”protecting against replay attacks.
 *
 * Verifies the issuer of the token by cross-checking it with active OpenID providers
 * using [openid-providers.get.hl]. If the issuer is not recognized, access is denied.
 *
 * If the token's email matches a user in the local 'users' table, the system proceeds
 * to authenticate them and attach additional profile metadata from 'users_extra'.
 *
 * If no user exists, this is considered a first-time login. A new user is created
 * with a default role of 'guest', and the email and optional name are stored as metadata.
 *
 * Finally, the user is authenticated using [magic.auth.authenticate], skipping password
 * verification (as it's an external identity provider), and a JWT token along with any
 * collected extra info is returned to the caller.
 */
.arguments
   token:string

/*
 * Verifying token.
 *
 * Notice, this will throw an exception if token is not valid.
 */
auth.token.verify
   token:x:@.arguments/*/token

/*
 * Verifying nonce from token to avoid replay attacks.
 */
cache.get:x:@auth.token.verify/*/nonce
if
   neq:x:@cache.get
      get-value:x:@auth.token.verify/*/issuer
   .lambda

      // Oops ...!!
      throw:Nonce is not valid!
         status:int:400
         public:bool:true

// Removing nonce from cache to avoid replay attacks.
cache.set:x:@auth.token.verify/*/nonce

// Verifying issuer is active.
.active:bool:false
execute-file:/system/auth/openid-providers.get.hl
   createNonce:bool:false
for-each:x:@execute-file/*
   if
      eq:x:@.dp/#/*/issuer
         get-value:x:@auth.token.verify/*/issuer
      .lambda
         set-value:x:@.active
            .:bool:true
if
   eq:x:@.issuer
      .:bool:false
   .lambda

      // Oops ...!
      throw:Not a recognized OpenID provider
         status:400
         public:bool:true

// Buffer for extra information.
.extra

// External OpenID provider has been verified, now we need to check to see if user exists in local database.
data.connect:magic

   // Checking if user exists.
   data.read
      table:users
      where
         and
            username.eq:x:@auth.token.verify/*/email
   if
      exists:x:@data.read/*
      .lambda

         /*
          * User has been previously created.
          *
          * Invoking [magic.auth.authenticate] that does the heavy lifting, and creates our JWT token,
          * making sure we return that token to caller.
          *
          * Notice, we ignore the password!
          */
         execute:magic.auth.authenticate
            password-check:bool:false
            username:x:@auth.token.verify/*/email

         // Retrieving extra information.
         data.connect:magic
            data.read
               table:users_extra
               where
                  and
                     user.eq:x:@auth.token.verify/*/email
                     or
                        type.eq:name
                        type.eq:email
            for-each:x:@data.read/*
               set-name:x:./*/add/*/*
                  get-value:x:@.dp/#/*/type
               set-value:x:./*/add/*/*
                  get-value:x:@.dp/#/*/value
               add:x:@.extra
                  .
                     .

         // Returns the authentication JWT ticket created above to caller.
         yield
            ticket:x:@execute/*/ticket
            extra:x:@.extra/*

   // First time login, creating a new user with username being email address.
   data.create
      table:users
      values
         username:x:@auth.token.verify/*/email
         password:openid-connect

   // Associating user with "guest" role.
   data.create
      table:users_roles
      values
         user:x:@auth.token.verify/*/email
         role:guest

   // Storing email as extra field.
   data.create
      table:users_extra
      values
         user:x:@auth.token.verify/*/email
         type:email
         value:x:@auth.token.verify/*/email

   // Checking to see if we've got a name value, and if we do we associate it with the user we create.
   if
      exists:x:@auth.token.verify/*/name
      .lambda

         // We've got name, storing it.
         data.create
            table:users_extra
            values
               user:x:@auth.token.verify/*/email
               type:name
               value:x:@auth.token.verify/*/name

   // Retrieving extra information.
   data.connect:magic
      data.read
         table:users_extra
         where
            and
               user.eq:x:@auth.token.verify/*/email
               or
                  type.eq:name
                  type.eq:email
      for-each:x:@data.read/*
         set-name:x:./*/add/*/*
            get-value:x:@.dp/#/*/type
         set-value:x:./*/add/*/*
            get-value:x:@.dp/#/*/value
         add:x:@.extra
            .
               .

/*
 * Invoking [magic.auth.authenticate] that does the heavy lifting, and creates our JWT token,
 * making sure we return that token to caller.
 */
execute:magic.auth.authenticate
   username:x:@auth.token.verify/*/email
   password-check:bool:false

// Returns the authentication JWT ticket created above to caller.
yield
   ticket:x:@execute/*/ticket
   extra:x:@.extra/*
