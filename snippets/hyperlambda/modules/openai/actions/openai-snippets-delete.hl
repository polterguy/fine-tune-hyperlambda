
/*
 * Deletes all training snippets from the specified [type].
 *
 * If [meta] is specified, the action will only delete snippets with specified meta type,
 * otherwise it will delete all snippets from [type].
 */
.arguments
   type:string
   meta:string

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/type

// Deleting snippets specified [type].
data.connect:[generic|magic]

   // Checking if we've got a [meta] argument.
   if:x:@.arguments/*/meta

      // [meta] argument was specified.
      data.execute:"delete from vss_ml_training_snippets where rowid in (select id as rowid from ml_training_snippets where type = @type and meta = @meta)"
         @type:x:@.arguments/*/type
         @meta:x:@.arguments/*/meta
      data.delete
         table:ml_training_snippets
         where
            and
               type.eq:x:@.arguments/*/type
               meta.eq:x:@.arguments/*/meta

   else

      // [meta] argument was not specified.
      data.execute:"delete from vss_ml_training_snippets where rowid in (select id as rowid from ml_training_snippets where type = @type)"
         @type:x:@.arguments/*/type
      data.delete
         table:ml_training_snippets
         where
            and
               type.eq:x:@.arguments/*/type
