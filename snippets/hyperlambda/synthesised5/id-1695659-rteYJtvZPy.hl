
/*
 * 1. [cache.try-get]: Attempts to retrieve a cached result using the key generated by [strings.concat]; if not found, executes the child lambda and caches the result for 36,000 seconds.
 * 2. [strings.concat]: Dynamically constructs slot names based on the [databaseType] to invoke the correct "magic.db.[type].databases" slot, and similarly for tables, columns, foreign_keys, and indexes.
 * 3. [unwrap] and [signal]: [unwrap] evaluates the concatenated slot name and [signal] invokes the corresponding slot with the provided connection string and database/table as needed, retrieving schema metadata.
 * 4. [switch]: Depending on [databaseType], removes system or template databases from results to ensure only user databases are processed.
 * 5. [for-each] and [add]: Iteratively processes each database, then each table, then each column/foreign key/index, assembling a hierarchical structure representing the full schema (databases -> tables -> columns/foreign_keys/indexes).
 * 6. [lambda2hyper] and [return]: Converts the constructed node structure into a Hyperlambda-compatible format and returns it.
 * 
 * Use cases:
 * - Generating a full hierarchical representation of all user databases, tables, columns, foreign keys, and indexes for a given SQL server, for use in migration, documentation, or code generation tools.
 * - Building a dynamic database explorer in a UI, where the structure is retrieved and cached for performance.
 * - Filtering out system databases and internal tables to focus only on application-relevant schema metadata.
 * - Abstracting over different SQL database types (MSSQL, MySQL, PostgreSQL) to provide uniform schema introspection regardless of backend.
 * - Supporting advanced scenarios like automated schema analysis, comparison, or auditing by returning a detailed, normalized schema tree.
 */
cache.try-get:x:@strings.concat
   expiration:36000
   .lambda
      .databaseType:x:@.arguments/*/databaseType
      .connection-string:x:@.arguments/*/connectionString
      strings.concat
         .:magic.db.
         get-value:x:@.databaseType
         .:.databases
      unwrap:x:+/*
      signal:x:@strings.concat
         connection-string:x:@.connection-string
      switch:x:@.databaseType
         case:mssql
            remove-nodes:x:@signal/*/*/db/=master
            remove-nodes:x:@signal/*/*/db/=tempdb
            remove-nodes:x:@signal/*/*/db/=model
            remove-nodes:x:@signal/*/*/db/=msdb
         case:mysql
            remove-nodes:x:@signal/*/*/db/=information_schema
            remove-nodes:x:@signal/*/*/db/=mysql
            remove-nodes:x:@signal/*/*/db/=performance_schema
            remove-nodes:x:@signal/*/*/db/=sys
         case:pgsql
            remove-nodes:x:@signal/*/*/db/=template1
            remove-nodes:x:@signal/*/*/db/=template0
      .result
      for-each:x:@signal/*/*
         set-name:x:@.dp/#
            .:database
         unwrap:x:+/*/*/*
         add:x:@.result
            .
               .
                  name:x:@.dp/#
                  tables
      for-each:x:@.result/*
         strings.concat
            .:magic.db.
            get-value:x:@.databaseType
            .:.tables
         unwrap:x:+/*
         signal:x:@strings.concat
            connection-string:x:@.connection-string
            database:x:@.dp/#/*/name
         for-each:x:@signal/*
            unwrap:x:+/*/*/*
            add:x:@for-each/@.dp/#/*/tables
               .
                  .
                     name:x:@.dp/#/*/table
                     columns
            strings.concat
               .:magic.db.
               get-value:x:@.databaseType
               .:.columns
            unwrap:x:+/*
            signal:x:@strings.concat
               connection-string:x:@.connection-string
               database:x:@for-each/@.dp/#/*/name
               table:x:@.dp/#/*/table
            for-each:x:@signal/*
               unwrap:x:+/*/*/*
               add:x:@for-each/@for-each/@.dp/#/*/tables/0/-/*/columns
                  .
                     .
                        name:x:@.dp/#/*/name
                        db:x:@.dp/#/*/db
                        nullable:x:@.dp/#/*/nullable
                        primary:x:@.dp/#/*/primary
                        automatic:x:@.dp/#/*/automatic
                        hl:x:@.dp/#/*/hl
            add:x:@for-each/@for-each/@.dp/#/*/tables/0/-
               .
                  foreign_keys
            strings.concat
               .:magic.db.
               get-value:x:@.databaseType
               .:.foreign_keys
            unwrap:x:+/*
            signal:x:@strings.concat
               connection-string:x:@.connection-string
               database:x:@for-each/@for-each/@.dp/#/*/name
               table:x:@.dp/#/*/table
            for-each:x:@signal/*
               add:x:+/*/*
                  get-nodes:x:@.dp/#/*
               add:x:@for-each/@for-each/@for-each/@.dp/#/*/tables/0/-/*/foreign_keys
                  .
                     .
            add:x:@for-each/@for-each/@for-each/@.dp/#/*/tables/0/-
               .
                  indexes
            strings.concat
               .:magic.db.
               get-value:x:@.databaseType
               .:.indexes
            unwrap:x:+/*
            signal:x:@strings.concat
               connection-string:x:@.connection-string
               database:x:@for-each/@for-each/@for-each/@.dp/#/*/name
               table:x:@.dp/#/*/table
            for-each:x:@signal/*
               add:x:+/*/*
                  get-nodes:x:@.dp/#/*
               add:x:@for-each/@for-each/@for-each/@for-each/@.dp/#/*/tables/0/-/*/indexes
                  .
                     .
      lambda2hyper:x:@.result/*
      return:x:@lambda2hyper
