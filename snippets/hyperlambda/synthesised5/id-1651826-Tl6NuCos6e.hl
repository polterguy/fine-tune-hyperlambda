
// Opens a connection to the "magic" database using the "generic" connection string, checks if a machine learning model type already exists in the "ml_types" table and throws an error if it does, triggers a signal to check if a model can be created, and if the "autocrawl" argument is set, adds the base URL to the creation values. It sets token limits based on the model type, then inserts a new record into "ml_types" with various configuration fields for the new machine learning model, including system message and conversation starters.
data.connect:[generic|magic]
   data.read
      table:ml_types
      columns
         type
      where
         and
            type.eq:x:@.type
   if
      exists:x:@data.read/*
      .lambda
         throw:Model already exists!
   try-signal:magic.ai.can-create-machine-learning-model
   if:x:@.arguments/*/autocrawl
      unwrap:x:+/*/*
      add:x:@data.connect/*/data.create/[0,1]/*/values
         .
            base_url:x:@.arguments/*/url
   .max-tokens:int:4000
   .max-context-tokens:int:4000
   if
      not
         strings.starts-with:x:@.arguments/*/model
            .:gpt-4
      .lambda
         set-value:x:@.max-context-tokens
            .:int:1000
         set-value:x:@.max-tokens
            .:int:1000
   data.create
      table:ml_types
      values
         type:x:@.type
         model:x:@.arguments/*/model
         max_tokens:x:@.max-tokens
         max_context_tokens:x:@.max-context-tokens
         max_request_tokens:int:1000
         temperature:decimal:0.3
         recaptcha:decimal:0.0
         supervised:int:1
         cached:int:0
         use_embeddings:int:1
         threshold:decimal:0.3
         vector_model:text-embedding-ada-002
         system_message:x:@.arguments/*/flavor
         greeting:Hi there, how can I help you?
         conversation_starters:"* What can I ask you about?\n* How do I contact you?\n* Who created this chatbot?"
