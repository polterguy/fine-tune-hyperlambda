/*
 * How do I create an HTTP endpoint that allows me to read items from my database with optional filtering arguments?
 */
.arguments

   // This argument becomes mandatory due to the [validators.mandatory] invocation below.
   status:int

   // This becomes dynamically added to [and] below resulting in having optional filtering on the column
   name.eq:string

   // This becomes dynamically added to [and] below resulting in having optional filtering on the column
   email.eq:string

   // This becomes dynamically added to [and] below resulting in having optional filtering on the column
   phone.eq:string

validators.mandatory:x:@.arguments/*/status

data.connect:YOUR_DATABASE

   // Adding all filtering arguments to below [and]
   add:x:./*/data.read/*/where/*/and
      get-nodes:x:@.arguments/*/name.eq
      get-nodes:x:@.arguments/*/email.eq
      get-nodes:x:@.arguments/*/phone.eq

   data.read
      table:YOUR_TABLE
      where
         and
            // This argument is mandatory and filter on status column using equality comparison
            status.eq:x:@.arguments/*/status

            // This lambda segment is dynamically populated with the above [add]

