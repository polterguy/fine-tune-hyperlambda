
/*
 * How do I create an API endpoint that inserts one record into the Album table in the chinook database
 *
 * Endpoint can only be executed by users belonging to the 'guest' role.
 * Endpoint connects to SQLite database and returns 'success' if successful.
 */
.arguments

   // AlbumId column value
   AlbumId:long

   // Title column value
   Title:string

   // ArtistId column value
   ArtistId:long

// Verifying user is authorized to access endpoint.
auth.ticket.verify:guest

// Opening up database connection.
data.connect:[generic|chinook]
   database-type:sqlite

   // Parametrising our create invocation.
   add:x:./*/data.create/*/values
      get-nodes:x:@.arguments/*

   // Creating our record.
   data.create
      database-type:sqlite
      table:Album
      values

   // Returning the correct status code.
   response.status.set:201

   // Returning result of above invocation to caller.
   return
      result:success
