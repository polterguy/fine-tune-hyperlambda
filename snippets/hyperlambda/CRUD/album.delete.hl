
/*
 * Create a web API endpoint that deletes one record from the Album table in the SQLite chinook database matching the specified [AlbumId] argument.
 *
 * The endpoint should ONLY be allowed to be invoked by 'root' users.
 */
.arguments

   // Primary key of record to delete, mandatory argument
   AlbumId:long

// Verifying user is authorized to access endpoint.
auth.ticket.verify:root

// AlbumId is mandatory!
validators.mandatory:x:@.arguments/*/AlbumId

data.connect:[generic|chinook]
   database-type:sqlite

   // Parametrising our delete invocation.
   add:x:./*/data.delete/*/where/*/and
      get-nodes:x:@.arguments/*

   // Deleting record matching specified AlbumId argument.
   data.delete
      database-type:sqlite
      table:Album
      where
         and

   // Returning how many records was affected by above invocation.
   yield
      affected:x:@data.delete
